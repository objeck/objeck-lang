#~
` HTML code documentation system
Copyright (c) 2014, 2019, 2025 Randy Hollines
~#

use System.IO.Filesystem;
use Collection;

class HtmlMaker {
	@parser : SnippetParser;
	@css_path : String;
	@version : String;
	@bundle_snippit : BundleSnippet;
	
	#~
	Constructor for HtmlMaker
	@param args command-line arguments [css_path, version]
	@return instance of HtmlMaker
	~#
	New(args : String[]) {
		@css_path := args[0];
		@version := args[1];
		@parser := SnippetParser->New(args);
	}
	
	#~
	Constructor for HtmlMaker
	@param args command-line arguments [css_path, version]
	@return instance of HtmlMaker
	~#
	function : Main(args : String[]) ~ Nil {
		if(args->Size() > 2) {
			HtmlMaker->New(args)->Make();
		};
	}
	
	#~
	Constructor for HtmlMaker
	@param args command-line arguments [css_path, version]
	@return instance of HtmlMaker
	~#
	method : Make() ~ Bool {
		if(<>@parser->Parse()) {
			"--- Unable to parse source file ---"->ErrorLine();
			return false;
		};

		# fiiles -> bundles -> (uses)
		
		template_path := String->New(@css_path);
		template_path += '/';
		template_path += "html_tmpl.dat";
		template_html := FileReader->ReadFile(template_path);
		if(template_html = Nil) {
			"--- Unable load header CSS template ---"->ErrorLine();
			return false;
		};
				
		## bundles
		bundles := @parser->GetBundles();
		nav_html := CreateNav(bundles);

		CreateBundles(bundles, nav_html, template_html);

		bundle_keys := bundles->GetKeys()<String>;

		"Writing API files..."->PrintLine();
		each(i : bundle_keys) {
			System.Utility.Spinner->Next();

			@bundle_snippit := bundles->Find(bundle_keys->Get(i));

			# get file uses
			filename := @bundle_snippit->GetFileName();
			
			# "filename='{$filename}'"->PrintLine();
			
			file_uses := @parser->GetFileUses(filename)<String>;

			# process classes
			classes := @bundle_snippit->GetClasses();
			class_keys := classes->GetKeys()<String>;
			each(j : class_keys) {
				class_block := classes->Find(class_keys->Get(j));
				if(class_block <> Nil) {
					CreateClassHtml(class_block, file_uses, nav_html, template_html);
				};
			};
			
			# process enums
			enums := @bundle_snippit->GetEnums();
			enum_keys := enums->GetKeys()<String>;
			each(j : enum_keys) {
				##  enum
				enum_block := enums->Find(enum_keys->Get(j));
				if(enum_block <> Nil) {
					CreateEnumHtml(enum_block, file_uses, nav_html, template_html);
				};
			};			
		};
		System.Utility.Spinner->Last();
		
		return true;
	}
	
	#~
	Generates the HTML file for a class
	@param class_block class metadata
	@param file_uses set of referenced bundles
	@param nav_html shared nav markup
	@param template_html base HTML template
	~#
	method : CreateClassHtml(class_block : ClassSnippet, file_uses : Set<String>, nav_html : String, template_html : String) ~ Nil {
		# set nav
		html := template_html->ReplaceAll("@version", @version);
		html := html->ReplaceAll("@nav", nav_html);

		# set main
		main_html := "<h2>";
		if(class_block->IsInterface()) {
			main_html += "<i>";
			main_html += class_block->GetShortName();
			main_html += "</i>";
		}
		else {
			main_html += class_block->GetShortName();
		};
		
		main_html += "</h2><p>";
		main_html += class_block->GetDesc();
		main_html += "</p>";

		if(class_block->GetCode() <> Nil) {
			main_html += "<pre><code class='language-cpp'>";
			main_html += class_block->GetCode();
			main_html += "</pre></code>";
		};

		func_names := class_block->GetFunctionNames();
		if(<>func_names->IsEmpty()) {
			main_html += "<h3>Operations</h3>";

			main_html += "<ul>";
			
			# write 'New' methods first
			each(func_name in func_names) {
				func_link := class_block->GetShortName()->ToLower() + '-' + func_name->ToLower();
				if(func_name->StartsWith("New")) {
					main_html += "<li><a href='#"
					main_html += func_link;
					main_html += "'>";
					main_html += func_name;
					main_html += "</a></li>";
				};
			};

			# ...and the rest of the lot
			each(func_name in func_names) {
				func_link := class_block->GetShortName()->ToLower() + '-' + func_name->ToLower();
				if(<>func_name->StartsWith("New")) {
					main_html += "<li><a href='#"
					main_html += func_link;
					main_html += "'>";
					main_html += func_name;
					main_html += "</a></li>";
				};
			};
			
			main_html += "</ul>";
		}
		else {
			main_html += "<h3>No Defined Operations</h3>";
		};

      	func_names := class_block->GetFunctionNames();
		each(func_name in func_names) {
			funcs := class_block->GetFunctions(func_name);
			each(func in funcs) {
				func_name := func->GetName();
				func_desc := func->GetDesc();
				func_sig := func->GetSignature();

				func_link := class_block->GetShortName()->ToLower() + '-' + func_name->ToLower();

				main_html += "<div class='grey-block'><a id='";
				main_html += func_link;
				main_html += "'></a><h3>";
      			
      			main_html += func_name;
      			main_html += "</h3><p>";
      			main_html += func_desc;
      			
      			main_html += "</p><code>"
      			main_html += func_sig;
      			main_html += "</code>";
      			# write parameters

      			param_tags := func->GetParamTags();
				param_names := func->GetParams();

				if(<>param_names->IsEmpty()) {
	      			main_html += "<p><b>Parameters</b></p><table><tr><th>Name</th><th>Type</th><th>Description</th></tr>";

					each(param in param_names) {
						main_html += "<tr>";

						param_tag := param_tags->Find(param->GetName())->As(SnippetTag);
						if(param_tag <> Nil & <>param_tag->IsIgnore()) {	      				
		      				param_name := param_tag->GetName();
		      				param_type_name := param->GetTypeName();
		      				param_type_desc := param_tag->GetDesc();

			      			main_html += "<td>";
			      			main_html += param_name;
			      			main_html += "</td>";
			      			
			      			main_html += "<td>";
			      			main_html += GetTypeLink(param_type_name, file_uses);
			      			main_html += "</td>";
			      			
			      			main_html += "<td>";
			      			main_html += param_type_desc;
			      			main_html += "</td>";
			      		};

			      		main_html += "</tr>";
			      	};

	      			main_html += "</table>";
	      		};

	      		if(func->GetReturnTag() <> Nil & func->GetReturnType() <> Nil) {
	      			main_html += "<p><b>Return</b><table><tr><th>Type</th><th>Description</th></tr>";

	      			rtrn_type_name := func->GetReturnType()->GetTypeName();
      				rtrn_desc := func->GetReturnTag()->GetDesc();

	      			main_html += "<tr><td>";
	      			main_html += GetTypeLink(rtrn_type_name, file_uses); ;
	      			main_html += "</td>";

	      			main_html += "<td>";
	      			main_html += rtrn_desc;
	      			main_html += "</td>";

	      			main_html += "</tr></table>";
	      		};
	      		main_html += "<br /></div>";
			}
		};

		# write file	
		html := html->ReplaceAll("@main", main_html);

		filename := GetClassLink(class_block, "../html/");
		# "=> {$filename}"->PrintLine();

		out := FileWriter->New(filename);
		out->WriteString(html);
		out->Close();
	}

	#~
	Generates the HTML file for an enum
	@param enum_block enum metadata
	@param file_uses set of referenced bundles
	@param nav_html shared nav markup
	@param template_html base HTML template
	~#
	method : CreateEnumHtml(enum_block : EnumConstSnippet, file_uses : Set<String>, nav_html : String, template_html : String) ~ Nil {
		# set nav
		html := template_html->ReplaceAll("@version", @version);
		html := html->ReplaceAll("@nav", nav_html);
		
		# set main
		main_html := "<h2>";
		main_html += enum_block->GetName();
		main_html += "</h2><p>";
		main_html += enum_block->GetDesc();
		main_html += "</p><h3>Items</h3><ul>";

		enum_items := enum_block->GetItems();
		each(enum_item in enum_items) {
			main_html += "<li>";
			main_html += enum_item;
			main_html += "</li>";
		};

		# write file	
		html := html->ReplaceAll("@main", main_html);

		filename := GetEnumLink(enum_block, "../html/");
		# "=> {$filename}"->PrintLine();

		out := FileWriter->New(filename);
		out->WriteString(html);
		out->Close();
	}

	method : GetTypeLink(name : String, file_uses : Set<String>) ~ String {
		decoded_name := name->ReplaceAll("&lt;", "<")->ReplaceAll("&gt;", ">");
		# "type-name: '{$decoded_name}'"->PrintLine();
		
		bundle_str : String; 
		class_enum_str : String; 
		enum_value_str : String;

		# parse bundle and class/enum
		class_index := decoded_name->Find('.');
		if(class_index > -1) {
			bundle_str := decoded_name->SubString(class_index);
			class_index += 1;
			class_enum_str := decoded_name->SubString(class_index, decoded_name->Size() - class_index);
		}
		else {
			# parse class with enum
			enum_index := class_index := decoded_name->Find("->");
			if(enum_index > -1) {
				class_enum_str := decoded_name->SubString(enum_index);
				enum_index += 2;
				enum_value_str := decoded_name->SubString(enum_index, decoded_name->Size() - enum_index);
			}
			else {
				class_enum_str := decoded_name;
			};
		};

		array_dim := 0;
		if(class_enum_str <> Nil & class_enum_str->Has('[')) {
			array_index := class_enum_str->Find('[');
			temp_str := class_enum_str->SubString(array_index);
			array_dim := class_enum_str->SubString(array_index, class_enum_str->Size() - array_index)->Count('[');
			class_enum_str := temp_str;
		};

		#
		# bundle and class/enum
		#
		if(bundle_str <> Nil & class_enum_str <> Nil & enum_value_str = Nil) {
			bundles := @parser->GetBundles();

			bundle_block := bundles->Find(bundle_str);
			if(bundle_block <> Nil) {
				classes := bundle_block->GetClasses();

				generic_index := class_enum_str->Find('<');
				if(generic_index > -1) {
					return GetComplexTypeLink(decoded_name, file_uses);
				}
				else {
					class_block := classes->Find(class_enum_str);
					if(class_block <> Nil) {
						class_link := "<a href='"
						class_link += GetClassLink(class_block);
						
						link_target := bundle_block->GetName()->ToLower();
						class_link += "?scrollTo={$link_target}'>";
						
						class_link += class_enum_str;
						class_link += "</a>";

						# "class_link=\"{$class_link}\""->PrintLine();
						return class_link;
					};					
				};
			};
		}
		#
		# class with enum
		#
		else if(class_enum_str <> Nil & enum_value_str <> Nil) {
			class_enum_key := class_enum_str->Copy();
			class_enum_key += "-&gt;";
			class_enum_key += enum_value_str;

			class_link := GetClassEnumLink(class_enum_str, file_uses);
			if(class_link = Nil) {
				class_link := String->New(class_enum_str);
			};

			enum_link := GetClassEnumLink(class_enum_key, file_uses);
			if(enum_link = Nil) {
				enum_link := String->New(enum_value_str);
			};
			
			enum_link_str := class_link + "-&gt;" + enum_link;
			# "enum_link=\"{$enum_link_str}\""->PrintLine();

			return enum_link_str;
		}
		#
		# class/enum
		#
		else if(class_enum_str <> Nil) {
			generic_index := class_enum_str->Find('<');
			if(generic_index > -1) {
				return GetComplexTypeLink(decoded_name, file_uses);
			}
			else {
				class_enum_link := GetClassEnumLink(class_enum_str, file_uses);
				if(class_enum_link <> Nil) {
					return class_enum_link;
				};

				return name;
			};
		};

		if(decoded_name->Has('<') | decoded_name->Has('[') | decoded_name->Has('~') | decoded_name->Has(',')) {
			return GetComplexTypeLink(decoded_name, file_uses);
		};

		# resolve within file
		if(bundle_str <> Nil & class_enum_str <> Nil ) {
			bundle_class_index := class_enum_str->FindLast('.');
			if(bundle_class_index > -1) {
				bundle_part_str := class_enum_str->SubString(bundle_class_index);
				bundle_class_index += 1;
				class_part_str := class_enum_str->SubString(bundle_class_index, class_enum_str->Size() - bundle_class_index);

				bundle_blocks := @parser->GetBundles();
				found_bundle_block := bundle_blocks->Find(bundle_str + '.' + bundle_part_str);
				if(found_bundle_block <> Nil) {
					bundle_classes := found_bundle_block->GetClasses();
					found_class_block := bundle_classes->Find(class_part_str);
					if(found_class_block <> Nil) {
						class_link := "<a href='"
						class_link += GetClassLink(found_class_block);
						
						link_target := class_part_str->ToLower();
						class_link += "?scrollTo={$link_target}'>";
						
						class_link += class_part_str;
						class_link += "</a>";

						# "class_link=\"{$class_link}\""->PrintLine();
						return class_link;
					};
				};
			};
		}

		# "class_link=\"{$name}\""->PrintLine();

		return name;
	}

	method : GetClassEnumLink(class_enum_str : String, file_uses : Set<String>) ~ String {
		bundles := @parser->GetBundles();
		
		bundle_keys := bundles->GetKeys()<String>;
		each(bundle_key in bundle_keys) {
			# look at bundles that are in our scope
			bundle_snippit := bundles->Find(bundle_key);
			if(file_uses->Has(bundle_key)) {
				# check classes
				classes := bundle_snippit->GetClasses();
				class_keys := classes->GetKeys()<String>;
				each(class_key in class_keys) {
					# "class-check='{$class_enum_str}', key='{$class_key}'"->PrintLine();
					
					# find class within the bundle scope
					class_block := classes->Find(class_enum_str);
					if(class_block <> Nil) {
						class_link := "<a href='"
						class_link += GetClassLink(class_block);

						link_target	:= bundle_snippit->GetName()->ToLower();
						class_link += "?scrollTo={$link_target}'>";
						
						class_link += class_enum_str;
						class_link += "</a>";

						# "class_link=\"{$class_link}\""->PrintLine();							
						return class_link;
					};
				};

				# check enums
				enums := bundle_snippit->GetEnums();
				enum_keys := enums->GetKeys()<String>;
				each(enum_key in enum_keys) {					
					# find enum within the bundle scope
					enum_block := enums->Find(class_enum_str);
					if(enum_block <> Nil) {
						class_enum_offset := class_enum_str->Find(';');
						if(class_enum_offset > -1) {
							class_enum_offset += 1;
							class_enum_str := class_enum_str->SubString(class_enum_offset, class_enum_str->Size() - class_enum_offset);
						};

						enum_link := "<a href='"
						enum_link += GetEnumLink(enum_block);

						link_target := bundle_snippit->GetName()->ToLower();
						enum_link += "?scrollTo={$link_target}'>";

						enum_link += class_enum_str;
						enum_link += "</a>";

						# "enum_link=\"{$enum_link}\""->PrintLine();							
						return enum_link;
					};
				};
			};
		};

		return Nil;
	}

	method : GetComplexTypeLink(decoded_name : String, file_uses : Set<String>) ~ String {
		class_link := ""; 
		temp := "";

		# "=> {$decoded_name}"->PrintLine();

		each(c in decoded_name) {
			if(c->IsChar() | c->IsDigit()) {
				temp += c;
			}
			else {
				select(c) {
					label '<': {
						if(<>temp->IsEmpty()) {
							class_link += GetTypeLink(temp, file_uses);
							temp := "";
						};
						class_link += "&lt;";
					}

					label '[': {
						if(<>temp->IsEmpty()) {
							class_link += GetTypeLink(temp, file_uses);
							class_link += '[';
							temp := "";
						};
					}

					label '(': {
						if(<>temp->IsEmpty()) {
							class_link += GetTypeLink(temp, file_uses);
							temp := "";
						};
						class_link += '(';
					}

					label ',': {
						if(<>temp->IsEmpty()) {
							class_link += GetTypeLink(temp, file_uses);
							temp := "";
						};
						class_link += ',';
					}

					label '~': {
						class_link += '~';
					}

					label '>': {
						if(<>temp->IsEmpty()) {
							class_link += GetTypeLink(temp, file_uses);
							temp := "";
						};
						class_link += "&gt;";
					}
							
					label ']': {
						if(<>temp->IsEmpty()) {
							class_link += GetTypeLink(temp, file_uses);
							temp := "";
						};
						class_link += ']';
					}

					label ')': {
						if(<>temp->IsEmpty()) {
							class_link += GetTypeLink(temp, file_uses);
							temp := "";
						};
						class_link += ')';
					}
				};
			};
		};

		if(<>temp->IsEmpty()) {
			class_link += GetTypeLink(temp, file_uses);
			temp := "";
		};

		# "class_link=\"{$class_link}\""->PrintLine();		
		return class_link;
	}

	#~
	Creates documentation for each bundle
	@param bundles map of bundles
	@param nav_html navigation block
	@param template_html HTML template
	~#
	method : CreateBundles(bundles : Map<String, BundleSnippet>, nav_html : String, template_html : String) ~ Nil {
		# write version file
		version_str := "";
		each(c in @version) {
			if(c->IsDigit()) {
				version_str += c;
			};
		};
		file_name := "../html/version.txt";
		out := FileWriter->New(file_name);
		out->WriteString(version_str);
		out->Close();

		# write bundle index
		html := template_html->ReplaceAll("@version", @version);
		html := html->ReplaceAll("@nav", nav_html);

		# set main
		main_html := "<table><tr><th>Name</th><th>Description</th></tr>";


		# process bundles
		bundle_keys := bundles->GetKeys()<String>;
		each(i : bundle_keys) {
			main_html += "<tr>";

			bundle_block := bundles->Find(bundle_keys->Get(i));
			if(bundle_block->IsParsed()) {
				link_target := bundle_block->GetName()->ToLower();
				block_name := bundle_block->GetName();

				link_target := bundle_block->GetName()->ToLower();
				main_html += "<td><a href='";
				main_html += "?scrollTo={$link_target}'</a>";
	  			main_html += block_name;
	  			main_html += "</td>";
	  			
	  			main_html += "<td>";
	  			main_html += bundle_block->GetDesc();
			  	main_html += "</td>";
			};

			main_html += "</tr>";
		};
		main_html += "</table>";

		# write file	
		html := html->ReplaceAll("@main", main_html);

		filename := "../html/index.html";
		# "=> {$filename}"->PrintLine();

		out := FileWriter->New(filename);
		out->WriteString(html);
		out->Close();
	}

	#~
	Builds the HTML navigation block
	@param bundles parsed bundle map
	@return HTML nav as a string
	~#
	method : CreateNav(bundles : Map<String, BundleSnippet>) ~ String {
		html := "<nav id='side-nav'>";

		# process bundles
		bundle_keys := bundles->GetKeys()<String>;
		each(i : bundle_keys) {
			bundle_block := bundles->Find(bundle_keys->Get(i));
			if(bundle_block->IsParsed()) {
				block_name := bundle_block->GetName();
				html += "<a id='";
				html += block_name->ToLower();
				html += "'></a><strong>";
				html += block_name;
				html += "</strong>";
				html += "<ul>";

				# process classes
				classes := bundle_block->GetClasses();
				class_keys := classes->GetKeys()<String>;			
				link_target := bundle_block->GetName()->ToLower();

				each(j : class_keys) {
					class_block := classes->Find(class_keys->Get(j));
					if(class_block->IsParsed()) {
						html += "<li><a href='";

						link_name := GetClassLink(class_block);
						html->Append(link_name);

						html += "?scrollTo={$link_target}'>";
						if(class_block->IsInterface()) {
							html->Append("<i>");
							html->Append(class_block->GetName());
							html->Append("</i>");
						}
						else {
							html->Append(class_block->GetName());
						};
						html += "</a></li>";
					};
				};
				
				# process enums
				enums := bundle_block->GetEnums();
				enum_keys := enums->GetKeys()<String>;
				each(j : enum_keys) {
					enum_block := enums->Find(enum_keys->Get(j));
					if(enum_block->IsParsed()) {
						html += "<li><a href='";
						link_name := GetEnumLink(enum_block);

						html->Append(link_name);
						html += "?scrollTo={$link_target}'>";
						html->Append(enum_block->GetName());
						html += " [#]</a></li>";
					};
				};
				
				html += "</ul>";			
			};
		};

		html += "</nav>";
		return html;		
	}

	method : GetClassLink(class_block : ClassSnippet, prefix : String := Nil) ~ String {
		filename := "";

		if(prefix <> Nil) {
			filename := prefix;
		}
		filename += class_block->GetBundleName()->ToLower();

		filename += '-';
		filename += class_block->GetFileName()->ToLower();

		link_name := String->New(filename)->ReplaceAll("&gt;", "$");;
		link_name += ".html";

		return link_name;
	}

	method : GetEnumLink(enum_block : EnumConstSnippet, prefix : String := Nil) ~ String {
		filename := "";

		if(prefix <> Nil) {
			filename := prefix;
		}
		filename += enum_block->GetBundleName()->ToLower();

		filename += '-';
		filename += enum_block->GetName()->ToLower();

		link_name := String->New(filename)->ReplaceAll("&gt;", "$");
		link_name += ".html";

		return link_name;
	}
}