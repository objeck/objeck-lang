#~~
# Regular expression engine
#
# Copyright (c) 2014-2020 Randy Hollines
~~#

use Collection.Generic;

#~
Regular expression engine (-lib regex.obl)
~#
bundle Query.RegEx {
	#~
	Regular expression engine.
	
	<p>Support for following matches:<p>
	<ul>
		<li>. &#8208; any</li>
		<li>? &#8208; optional</li>
		<li>* &#8208; zero or more repetitions</li>
		<li>+ &#8208; one or more repetitions</li>
		<li>^ &#8208; first single match</li>
		<li>$ &#8208; last single match</li>
		<li>group &#8208; (one|two|three)</li>
		<li>range &#8208; [start-end]</li>
	</ul>
	
	<p>Support for following special patterns:<p>
	<ul>
		<li>word &#8208; \w</li>
		<li>not word &#8208; \W</li>
		<li>digit &#8208; \d</li>
		<li>not digit &#8208; \D</li>
		<li>white space &#8208; \s</li>
		<li>not white space &#8208; \s</li>
		<li>repeat &#8208; {least, most}
	</ul>		
	~#
	
	class RegEx {
		# parsing
		@tokens : Char[];
		@token_pos : Int;
		@token : Char;		
		@expressions : Vector<Expression>;
		@error : String;
		@starts_anchor : Bool;
		
		# matching
		@input : Char[];
		@input_pos : Int;
		@input_char : Char;
		@is_exact : Bool;
		@is_backtracking : Bool;
		@backtrack_cache : Map<IntHolder, Expression>;
		@backtrack_positions : Vector<IntHolder>;
        
		#~
		Default constructor
		@param input regex pattern
		~#
		New(input : String) {
			@tokens := input->ToCharArray();
			@expressions := Vector->New()<Expression>;
			@backtrack_positions := Vector->New()<IntHolder>;
			@backtrack_cache := Map->New()<IntHolder, Expression>;

			Parse();
		}

		#~
		Check of the regex was parsed correctly
		@return true if parsed, false otherwise
		~#
		method : public : IsOk() ~ Bool {
			return @error = Nil;
		}
		
		#~
		Looks for an exact regex match
		@param input string to match against
		@return true if exact, false otherwise
		~#
		method : public : MatchExact(input : String) ~ Bool {
			return Evaluate(input, 0) > -1 & @is_exact;
		}
		
		#~
		Matches as much of the string as possible
		@param input string to match against
		@return matched string if found, empty string otherwise
		~#
		method : public : Match(input : String) ~ String {
			return Match(input, 0);
		}
		
		#~
		Matches as much of the string as possible
		@param input string to match against
		@param offset offset into the to match against
		@return matched string if found, empty string otherwise
		~#
		method : public : Match(input : String, offset : Int) ~ String {
			right := Evaluate(input, offset);
			if(right > -1) {
				return input->SubString(offset, right - offset);
			};
			
			return "";
		}
		
		#~
		Matches the first occurrence 
		@param input string to match against
		@return matched string if found, empty string otherwise
		~#
		method : public : native : FindFirst(input : String) ~ String {
			left := 0;
			while(left < input->Size()) {
				right := Evaluate(input, left);
				if(right > -1) {
					return input->SubString(left, right - left);
				};
				left += 1;
			};

			return "";
		}

		#~
		Finds all occurrences 
		@param input string to match against
		@return vector of string matches
		~#
		method : public : native : Find(input : String) ~ Vector<String> {
			matches := Vector->New()<String>;
			left := 0;
			while(left < input->Size()) {
				right := Evaluate(input, left);
				if(right > -1 & left <> right) {
# IO.Console->Print("\t=== left=")->Print(left)->Print(", right=")->Print(right)->PrintLine(" ===");
					if(left < right) {
						matches->AddBack(input->SubString(left, right - left));
						left := right - 1;
					};
				};
				left += 1;
			};

			return matches;
		}

		#~
		Replaces the first occurrence with the given string
		@param input string to match against
		@param replace string to replace the match with
		@return replaced string
		~#
		method : public : native : ReplaceFirst(input : String, replace : String) ~ String {
			left := 0;
			while(left < input->Size()) {
				right := Evaluate(input, left);
				if(right > -1 & left <> right) {
					# identify start and end of input string
					start := input->SubString(left);
					end := input->SubString(right, input->Size() - right);
					# insert the replacement string
					input := String->New(start);
					input->Append(replace);
					input->Append(end);	
					
					return input;
				};
				left += 1;
			};

			return input;
		}

		#~
		Replaces all occurrences of the given string
		@param input string to match against
		@param replace string to replace the match with
		@return replaced string
		~#
		method : public : native : ReplaceAll(input : String, replace : String) ~ String {
			left := 0;
			while(left < input->Size()) {
				right := Evaluate(input, left);
				if(right > -1 & left <> right) {
					# identify start and end of input string
					start := input->SubString(left);
					end := input->SubString(right, input->Size() - right);
					# insert the replacement string
					input := String->New(start);
					input->Append(replace);
					input->Append(end);	
					# update search index
					left += replace->Size() - 1;
				};
				left += 1;
			};

			return input;
		}

		method : native : Evaluate(input : String, offset : Int) ~ Int {
			if(@error <> Nil) {
				@error->PrintLine();
				return -1;
			};

			if(offset >= input->Size()) {
				return -1;
			};
			
			# reset everything
			@is_exact := false;
			@is_exact := false;
			@is_backtracking := false;
			@backtrack_cache->Empty();
			@backtrack_positions->Empty();

			# process string
			@input_pos := offset;			
			@input := input->ToCharArray();			
			NextInput();

			# match with regex and check final position
			match := Evaluate(@expressions, 0);
# IO.Console->Print("@@@ pos=")->Print(@input_pos)->Print(", match=")->Print(match)->Print(", end=")->PrintLine(@input_char);
			if(match & @input_char <> '\0') {
				return @input_pos - 1;
			}
			else if(match) {
				@is_exact := true;
				return @input_pos;
			};
			
			return -1;
		}
		
		method : private : NextInput() ~ Nil {
			if(@input_pos < @input->Size()) {
				@input_char := @input[@input_pos];
				@input_pos += 1;
			}
			else {
				@input_char := '\0';
			};
		}	
		
		method : private : Evaluate(expressions : Vector<Expression>, offset : Int) ~ Bool {
			for(i := offset; i < expressions->Size(); i += 1;) {
				if(Evaluate(expressions->Get(i)) = false) {
					if(@is_backtracking) {
						@is_backtracking := false;
# IO.Console->Print("backtracking: token count=")->PrintLine(@backtrack_positions->Size());
						while(@backtrack_positions->IsEmpty() = false) {							
							@input_pos := @backtrack_positions->RemoveBack() - 1;
							NextInput();
							
							start := @input_pos;
							check := @backtrack_cache->Find(@input_pos);
							if(offset + 1 < expressions->Size() & check = expressions->Get(offset + 1)) {
								return true; 
							};

							if(Evaluate(expressions, offset + 1)) {
								if(offset + 1 < expressions->Size()) {
									@backtrack_cache->Insert(@input_pos, expressions->Get(offset + 1));
								};
								return true;
							};
						};
					};
					
					return false;
				};
			};
			
			return true;
		}

		method : private : InRange(char_class : String, check : Char) ~ Bool {
			return char_class->Get(0) <= check & char_class->Get(2) >= check;
		}

		method : private : InClass(char_class : String, check : Char) ~ Bool {
			for(i := 0; i < char_class->Size(); i += 1;) {
				if(char_class->Get(i) = check) {
					return true;
				};
			};
			
			return false;
		}
		
		method : native : Evaluate(expression : Expression) ~ Bool {
			if(expression <> Nil) {
				if(@is_backtracking & @input_char <> '\0') {
					@backtrack_positions->AddBack(@input_pos);
				};
				
# IO.Console->Print("--- type=")->Print(expression->GetType()->As(Int))->PrintLine(" ---");
 				
				select(expression->GetType()) {
					# single character
					label Expression->Type->CHAR: {
# IO.Console->Print("[### matching: expr='")->Print(expression->GetValue())->Print("', input='")->Print(@input_char)->PrintLine("' ###]");
						if(expression->GetValue() = @input_char) {
							NextInput();
							return true;
						};
					}
	
					# any character
					label Expression->Type->ANY: {
# "[### any ###]"->PrintLine();
						NextInput();
						return true;							
					}
					
					# digit
					label Expression->Type->DIGIT: {
# "[### digit ###]"->PrintLine();
						if(@input_char->IsDigit()) {
							NextInput();
							return true;
						};
					}

					# not digit
					label Expression->Type->NOT_DIGIT: {
# "[### not digit ###]"->PrintLine();
						if(<>@input_char->IsDigit()) {
							NextInput();
							return true;
						};
					}
					
					# word
					label Expression->Type->WORD: {
# "[### word ###]"->PrintLine();
						if(@input_char->IsDigit() | @input_char->IsChar() | @input_char = '_') {
							NextInput();
							return true;
						};
					}

					# not word
					label Expression->Type->NOT_WORD: {
# "[### not word ###]"->PrintLine();
						if(<>@input_char->IsDigit() & <>@input_char->IsChar() & <>@input_char = '_') {
							NextInput();
							return true;
						};
					}
					
					# whitespace
					label Expression->Type->WHITESPACE: {
# "[### whitespace ###]"->PrintLine();
						if(@input_char = ' ' | @input_char = '\t' | @input_char = '\r' | @input_char = '\n' | @input_char = 0xB) {
							NextInput();
							return true;
						};
					}

					# not whitespace
					label Expression->Type->NOT_WHITESPACE: {
# "[### not whitespace ###]"->PrintLine();
						if(<>@input_char = ' ' & <>@input_char = '\t' & <>@input_char = '\r' & <>@input_char = '\n' & <>@input_char = 0xB) {
							NextInput();
							return true;
						};
					}

					# class of characters
					label Expression->Type->CHAR_CLASS: {
						char_class := expression->GetClass();
# IO.Console->Print("[### matching range: expr='")->Print(expression->GetClass())->Print("', input='")->Print(@input_char)->PrintLine("' ###]");
						if(InClass(char_class, @input_char)) {
							NextInput();
							return true;
						};
					}

					# range of characters
					label Expression->Type->CHAR_CLASS_RANGE: {
# IO.Console->Print("[### matching: range='")->Print(expression->GetClass())->Print("', input='")->Print(@input_char)->PrintLine("' ###]");					
						char_class := expression->GetClass();
						if(InRange(char_class, @input_char)) {
							NextInput();
							return true;
						};
					}
					
					# starts anchor
					label Expression->Type->STARTS_ANCHOR: {
# IO.Console->Print("[### starts anchor: pos=")->Print(@input_pos - 1)->Print(", input='")->Print(@input_char)->PrintLine("' ###]");				
						if(@input_pos - 1 = 0 & Evaluate(expression->GetExpression())) {
							return true;				
						};
					}
					
					# ends anchor
					label Expression->Type->ENDS_ANCHOR: {
# "[### ends anchor ###]"->PrintLine();
						if(Evaluate(expression->GetExpression()) & @input_char = '\0') {
							return true;				
						};
					}

					# logical OR
					label Expression->Type->OR: {
# "[### or ###]"->PrintLine();					
						prev_pos := @input_pos;
						if(Evaluate(expression->GetLeft(), 0)) {
							return true;
						}
						else {
							@input_pos := prev_pos - 1;
							NextInput();
							return Evaluate(expression->GetRight(), 0);
						};
					}
	
					# zero or more
					label Expression->Type->ZERO_MORE: {
# "[### zero_or_more ###]"->PrintLine();
						@is_backtracking := true;
						if(expression->GetExpression()->GetType() = Expression->Type->SUB_EXPR) {
							do {
								match := Evaluate(expression->GetExpression()->GetLeft(), 0);
							} 
							while(@input_char <> '\0' & match);
						}
						else {
							if(expression->GetExpression()->GetType() = Expression->Type->ANY) {
								@is_backtracking := true;
							};
							
							do {	
								match := Evaluate(expression->GetExpression());
							}
							while(@input_char <> '\0' & match);
						};
						
						return true;
					}

					# one or more
					label Expression->Type->ONE_MORE: {
# "[### one_or_more ###]"->PrintLine();
						if(expression->GetExpression()->GetType() = Expression->Type->SUB_EXPR) {
							if(Evaluate(expression->GetExpression()->GetLeft(), 0)) {
								@is_backtracking := true;

								do {
									match := Evaluate(expression->GetExpression()->GetLeft(), 0);
								} 
								while(@input_char <> '\0' & match);

								return true;
							};
						}
						else {
							if(Evaluate(expression->GetExpression())) {
								@is_backtracking := true;
								
								do {	
									match := Evaluate(expression->GetExpression());
								}
								while(@input_char <> '\0' & match);

								return true;
							};
						};
					}
					
					# repeat
					label Expression->Type->REPEAT: {
# "[### repeat ###]"->PrintLine();
						count := 0;
						if(expression->GetExpression()->GetType() = Expression->Type->SUB_EXPR) {
							do {
								match := Evaluate(expression->GetExpression()->GetLeft(), 0);
								if(match) {
									count += 1;
								};
							} 
							while(@input_char <> '\0' & match);
						}
						else {
							if(expression->GetExpression()->GetType() = Expression->Type->ANY) {
								@is_backtracking := true;
							};
							
							do {	
								match := Evaluate(expression->GetExpression());
								if(match) {
									count += 1;
								};
							}
							while(@input_char <> '\0' & match);
						};
# IO.Console->Print("\tleast=")->Print(expression->GetLeast())->Print(", most=")->Print(expression->GetMost())->Print(", count=")->PrintLine(count);
						
						if(expression->GetMost() = -1) {
							return expression->GetLeast() >= count;
						}
						else {
							return expression->GetLeast() <= count & expression->GetMost() >= count;
						};
					}
					
					# single optional
					label Expression->Type->OPTIONAL: {
# "[### optional ###]"->PrintLine();
						if(expression->GetExpression()->GetType() = Expression->Type->SUB_EXPR) {
							Evaluate(expression->GetExpression()->GetLeft(), 0);
						}
						else {
							Evaluate(expression->GetExpression());
						};
						
						return true;
					}
					
					# sub-expression
					label Expression->Type->SUB_EXPR: {
						return Evaluate(expression->GetLeft(), 0);						
					}
				};
			};

			return false;
		}

		# ---------- parsing methods ----------
		method : private : NextToken() ~ Nil {
			if(@token_pos < @tokens->Size()) {
				@token := @tokens[@token_pos];
				@token_pos += 1;
			}
			else {
				@token := '\0';
			};
		}	

		method : private : Parse() ~ Nil {
			NextToken();
			Binary();
			
			# consume starts with
			if(@starts_anchor) {
				@error := "stray token";
			};
		}
		
		method : private : Binary() ~ Nil {
			# consume starts with
			if(@starts_anchor & @expressions->Size() > 0) {
				expression := @expressions->RemoveBack();
				@expressions->AddBack(Expression->New(Expression->Type->STARTS_ANCHOR, expression));
				@starts_anchor := false;
# IO.Console->Print("match: starts anchor; with=")->PrintLine(expression->GetType()->As(Int));	
			};
		
			while(@token <> '\0' & @token <> ')' & @token <> '|') {
				Unary();		
				if(@error <> Nil) {
					return;
				};
			};
			
			if(@token = '|') {
				NextToken();				
				
				left := @expressions;
				@expressions := Vector->New()<Expression>;
				
				Binary();
				if(@error <> Nil) {
					return;
				};
				
				right := @expressions;
				@expressions := Vector->New()<Expression>;
				
				@expressions->AddBack(Expression->New(left, right));
# IO.Console->PrintLine("match: or_match");					
			};
		}
		
		method : private : Unary() ~ Nil {
			# consume starts with
			if(@starts_anchor & @expressions->Size() > 0) {
				expression := @expressions->RemoveBack();
				@expressions->AddBack(Expression->New(Expression->Type->STARTS_ANCHOR, expression));
				@starts_anchor := false;
# IO.Console->Print("match: starts anchor; with=")->PrintLine(expression->GetType()->As(Int));	
			};
			
			Value();
			if(@error <> Nil) {
				return;
			};

			if(@token = '*') {
				if(@expressions->Size() > 0) {
					expression := @expressions->RemoveBack();
					@expressions->AddBack(Expression->New(Expression->Type->ZERO_MORE, expression));
					NextToken();
# IO.Console->PrintLine("match: zero_or_more");					
				}
				else {
					@error := "missing left-hand side operand";
					return;
				};
			}
			else if(@token = '+') {
				if(@expressions->Size() > 0) {
					expression := @expressions->RemoveBack();
					@expressions->AddBack(Expression->New(Expression->Type->ONE_MORE, expression));
					NextToken();
# IO.Console->PrintLine("match: one_or_more");					
				}
				else {
					@error := "missing left-hand side operand";
					return;
				};
			}
			else if(@token = '?') {
				if(@expressions->Size() > 0) {
					expression := @expressions->RemoveBack();
					@expressions->AddBack(Expression->New(Expression->Type->OPTIONAL, expression));
					NextToken();
# IO.Console->PrintLine("match: optional");					
				}
				else {
					@error := "missing left-hand side operand";
					return;
				};
			}
			else if(@token = '$') {
				if(@expressions->Size() > 0) {
					expression := @expressions->RemoveBack();
					@expressions->AddBack(Expression->New(Expression->Type->ENDS_ANCHOR, expression));
					NextToken();
# "match: ends anchor"->PrintLine();
				}
				else {
					@error := "missing left-hand side operand";
					return;
				};
			}	
			else if(@token = '{') {
				if(@expressions->Size() > 0) {
					NextToken();
					
					Whitespace();
					least := Number()->ToInt();
					if(@error <> Nil) {
						return;
					};
					
					Whitespace();
					most := -1;
					if(@token = ',') {
						NextToken();
						Whitespace();
						most := Number()->ToInt();
						if(@error <> Nil) {
							return;
						};
					};
					
					Whitespace();		
					if(@token <> '}') {
						@error := "expected '}'";
						return;
					};
					NextToken();
					
# IO.Console->Print("index: start='")->Print(least)->Print("', most='")->Print(most)->PrintLine("'");										
					
					expression := @expressions->RemoveBack();
					@expressions->AddBack(Expression->New(least, most, expression));
				}
				else {
					@error := "missing left-hand side operand";
					return;
				};
			};
		}
		
		method : private : Whitespace() ~ Nil {
			while(@token <> '\0' & (@token = ' ' | @token = '\t' | @token = '\r' | @token = '\n')) {
				NextToken();
			};
		}
		
		method : private : Number() ~ String {
			start := @token_pos - 1;
			while(@token <> '\0' & @token->IsDigit()) {
				NextToken();
			};
			
			if(start = @token_pos) {
				@error := "invalid number";
				return "";
			};
			
			return String->New(@tokens, start, @token_pos - start - 1);
		}
		
		method : private : Value() ~ Nil {
			# consume starts with
			if(@starts_anchor & @expressions->Size() > 0) {
				expression := @expressions->RemoveBack();
				@expressions->AddBack(Expression->New(Expression->Type->STARTS_ANCHOR, expression));
				@starts_anchor := false;
# IO.Console->Print("match: starts anchor; with=")->PrintLine(expression->GetType()->As(Int));	
			};		
		
			if(@token = '(') {
				Parentheses();				
			}
			else if(@token = '[') {
				CharacterClass();
			}
			else if(@token = '\\') {
				EscapeCharacter();
# "char: escaped"->PrintLine();
			}			
			else if(@token = '.') {
				@expressions->AddBack(Expression->New());
# "match: any"->PrintLine();			
				NextToken();
			}
			else if(@token = '^') {
				@starts_anchor := true;
				NextToken();
			}			
			else {
				select(@token) {
					label '*':
					label '+':
					label '?':
					label '$':
					label '{': { # nop processed by unary method
					}
					
					other: {
						@expressions->AddBack(Expression->New(@token));
# IO.Console->Print("match: char=")->PrintLine(@token);				
						NextToken();
					}
				};
			};
		}
		
		method : private : CharacterClass() ~ Nil {	
			NextToken();

			if(@token->IsChar() = false & @token->IsDigit() = false) {
				@error := "invalid character class";
			};
			
			start := @token_pos - 1;
			hyphen_count := 0;
			while(@token <> '\0' & @token <> ']' & (@token = '-' | @token->IsChar() | @token->IsDigit())) { 
				if(@token = '-') {
					hyphen_count += 1;
				};
				NextToken();
			};
			end := @token_pos - 1;
			char_class := String->New(@tokens, start, end - start);

			if(@token <> ']') {
				@error := "expected ')'";
				return;
			};
			NextToken();
			
			if(hyphen_count > 1) {
				@error := "invalid character class";
			};

			is_range := false;
			if(hyphen_count = 1) {
				if(char_class->Size() <> 3) {
					@error := "invalid character class";
					return; # to avoid an issue with next statement
				};
				
				if(char_class->Get(0) >= char_class->Get(2)) {
					@error := "invalid character class";
				};

				is_range := true;				
			};
# IO.Console->Print("match range: class=")->PrintLine(char_class);
			@expressions->AddBack(Expression->New(char_class, is_range));
		}

		method : private : EscapeCharacter() ~ Nil {
			NextToken();
			select(@token) {
				label '(':
				label ')':
				label '[':
				label ']':
				label '}':
				label '{':
				label '|':
				label '.':
				label '?':
				label '^':
				label '$': 
				label '*':
				label '+': {
					@expressions->AddBack(Expression->New(@token));
					NextToken();
				}
				
				label 'd': {
					@expressions->AddBack(Expression->New(Expression->Type->DIGIT));
					NextToken();
				}

				label 'D': {
					@expressions->AddBack(Expression->New(Expression->Type->NOT_DIGIT));
					NextToken();
				}
				
				label 'w': {
					@expressions->AddBack(Expression->New(Expression->Type->WORD));
					NextToken();
				}

				label 'W': {
					@expressions->AddBack(Expression->New(Expression->Type->NOT_WORD));
					NextToken();
				}
				
				label 's': {
					@expressions->AddBack(Expression->New(Expression->Type->WHITESPACE));
					NextToken();
				}

				label 'S': {
					@expressions->AddBack(Expression->New(Expression->Type->NOT_WHITESPACE));
					NextToken();
				}
				
				label 'n': {
					@expressions->AddBack(Expression->New('\n'));
					NextToken();
				}

				label 'r': {
					@expressions->AddBack(Expression->New('\r'));
					NextToken();
				}

				label 'b': {
					@expressions->AddBack(Expression->New('\b'));
					NextToken();
				}

				label 't': {
					@expressions->AddBack(Expression->New('\t'));
					NextToken();
				}

				label '\\': {
					@expressions->AddBack(Expression->New('\\'));
					NextToken();
				}
			};
		}

		method : private : Parentheses() ~ Nil {
			NextToken();
			
			left := @expressions;
			@expressions := Vector->New()<Expression>;
				
			Binary();
			if(@error <> Nil) {
				return;
			};
		
			if(@token <> ')') {
				@error := "expected ')'";
				return;
			};
			NextToken();
			
			# combine lists			
			left->AddBack(Expression->New(@expressions));
			@expressions := left;
# IO.Console->PrintLine("parentheses");		
		}
	}
	
	# ---------- subexpression value object ----------
	class Expression {
		# expression type
		enum Type := -100 {
			CHAR,
			ANY,
			OPTIONAL,
			DIGIT,
			NOT_DIGIT,
			WORD,
			NOT_WORD,
			WHITESPACE,
			NOT_WHITESPACE,
			CHAR_CLASS,
			CHAR_CLASS_RANGE,
			ENDS_ANCHOR,
			STARTS_ANCHOR,
			OR,
			ZERO_MORE,
			ONE_MORE,
			REPEAT,
			SUB_EXPR
		}

		@type : Expression->Type;
		@value : Char;
		@char_class : String;
		@expression : Expression;
		@left : Vector<Expression>;
		@right : Vector<Expression>;
		@least : Int;
		@most : Int;
		
		New() {
			@type := Expression->Type->ANY;
		}

		New(value : Char) {
			@type := Expression->Type->CHAR;
			@value := value;
		}

		New(least : Int, most : Int, expression : Expression) {
			@type := Expression->Type->REPEAT;
			@least := least;
			@most := most;
			@expression := expression;
		}
		
		New(char_class : String, is_range : Bool) {
			if(is_range) {
				@type := Expression->Type->CHAR_CLASS_RANGE;
			}
			else {
				@type := Expression->Type->CHAR_CLASS;
			};
			@char_class := char_class;
		}
		
		New(left : Vector<Expression>) {
			@type := Expression->Type->SUB_EXPR;
			@left := left;
		}
		
		New(type : Expression->Type) {
			@type := type;
		}
		
		New(type : Expression->Type, expression : Expression) {
			@type := type;
			@expression := expression;
		}

		New(left : Vector<Expression>, right : Vector<Expression>) {
			@type := Expression->Type->OR;
			@left := left;
			@right := right;
		}

		method : public : GetType() ~ Expression->Type {
			return @type;
		}
		
		method : public : GetValue() ~ Char {
			return @value;
		}

		method : public : GetClass() ~ String {
			return @char_class;
		}
		
		method : public : GetLeft() ~ Vector<Expression> {
			return @left;
		}
		
		method : public : GetRight() ~ Vector<Expression> {
			return @right;
		}
			
		method : public : GetLeast() ~ Int {
			return @least;
		}
		
		method : public : GetMost() ~ Int {
			return @most;
		}
		
		method : public : GetExpression() ~ Expression {
			return @expression;
		}
	}
}
