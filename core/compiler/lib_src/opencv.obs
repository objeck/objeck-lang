#~~
# TODO
# Copyright (c) 2025 Randy Hollines
# ONNX and OpenCV wrappers
~~#	

use System.API;

#~
Open Computer Vision (OpenCV) support for image rendering and compilation. (-lib onnx)
~#
bundle API.OpenCV {
	class : private : Proxy {
		@lib_proxy : static : DllProxy;
		
		function : GetDllProxy() ~ DllProxy {
			if(@lib_proxy = Nil) {
				@lib_proxy := DllProxy->New("libobjk_opencv");
			};

			return @lib_proxy;
		}
	}

	#~
	Image manipulation routines
	~#
	class Image {
		@image_type : Int;
		@image_cols : Int;
		@image_rows : Int;
		@image_bytes : Byte[];

		#~
		Image type
		~#
		enum Format := 64 {
			JPEG,
			PNG,
			WEBP,
			GIF
		}

		New : private() {
		}

		function : Load(image_path : String) ~ API.OpenCV.Image {
			array_args := Base->New[2];

			array_args[0] := Nil;
			array_args[1] := image_path;

			Proxy->GetDllProxy()->CallFunction("opencv_load_image_path", array_args);

			return array_args[0]->As(API.OpenCV.Image);
		}

		# TODO: test
		function : Load(image_bytes : Byte[]) ~ API.OpenCV.Image {
			array_args := Base->New[2];

			array_args[0] := Nil;
			array_args[1] := ByteArrayRef->New(image_bytes);

			Proxy->GetDllProxy()->CallFunction("opencv_load_image_bytes", array_args);

			return array_args[0]->As(API.OpenCV.Image);
		}

		method : public : Size() ~ Int {
			return @image_bytes->Size();
		}

		method : public : Rows() ~ Int {
			return @image_rows;
		}

		method : public : Columns() ~ Int {
			return @image_cols;
		}

		method : public : Type() ~ Int {
			return @image_type;
		}

		method : public : Bytes() ~ Byte[] {
			return @image_bytes;
		}

		method : public : Show(title : String) ~ Nil {
			array_args := Base->New[2];

			array_args[0] := @self;
			array_args[1] := title;

			Proxy->GetDllProxy()->CallFunction("opencv_show_image", array_args);
		}

		#~ TOOD: update
		Image format conversion
		@param input_image image bytes
		@param output_format image output format
		@return bytes for converted image
		~#
		method : public : Convert(format : Image->Format) ~ API.OpenCV.Image {			
			array_args := Base->New[3];

			array_args[0] := Nil;
			array_args[1] := @self;
			array_args[2] := IntRef->New(format->As(Int));

			Proxy->GetDllProxy()->CallFunction("opencv_convert_image", array_args);

			return array_args[0]->As(API.OpenCV.Image);
		}
	}
}