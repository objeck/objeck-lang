#~~
# TODO
# Copyright (c) 2025 Randy Hollines
# ONNX and OpenCV wrappers
~~#	

use System.API;

#~
Open Computer Vision (OpenCV) support for image rendering and compilation. (-lib onnx)
~#
bundle API.OpenCV {
	class : private : Proxy {
		@lib_proxy : static : DllProxy;
		
		function : GetDllProxy() ~ DllProxy {
			if(@lib_proxy = Nil) {
				@lib_proxy := DllProxy->New("libobjk_opencv");
			};

			return @lib_proxy;
		}
	}

	#~
	Image manipulation routines
	~#
	class Image {
		@image_bytes : Byte[]; # TODO: just store length
		@image_size_max : Int;

		#~
		Image type
		~#
		enum Format := 64 {
			JPEG,
			PNG,
			WEBP,
			GIF
		}

		New(image_bytes : Byte[]) {
			@image_bytes := image_bytes;
			@image_size_max := image_bytes->Size();
		}

		function : LoadFromFile(image_path : String) ~ API.OpenCV.Image {
			array_args := Base->New[2];

			array_args[0] := ByteArrayRef->New();
			array_args[1] := image_path;

			Proxy->GetDllProxy()->CallFunction("opencv_load_image_path", array_args);

			bytes_obj := array_args[0]->As(ByteArrayRef);
			if(bytes_obj <> Nil) {
				return API.OpenCV.Image->New(bytes_obj->Get());
			};

			return Nil;
		}

		method : public : Show() ~ Nil {
			array_args := Base->New[1];

			array_args[0] := ByteArrayRef->New(@image_bytes);

			Proxy->GetDllProxy()->CallFunction("opencv_show_image", array_args);
		}

		method : public : WriteToFile(image_path : String) ~  Bool {
			return false;
		}

		#~ TOOD: update
		Image format conversion
		@param input_image image bytes
		@param output_format image output format
		@return bytes for converted image
		~#
		method : public : Convert(output_format : Image->Format) ~ API.OpenCV.Image {
			array_args := Base->New[3];

			array_args[0] := ByteArrayRef->New();
			array_args[1] := ByteArrayRef->New(@image_bytes);
			array_args[2] := IntRef->New(output_format->As(Int));

			Proxy->GetDllProxy()->CallFunction("opencv_convert_image", array_args);
			
			holder := array_args[0]->As(ByteArrayRef);
			if(holder <> Nil) {
				return API.OpenCV.Image->New(holder->Get());
			};

			return Nil;
		}
	}
}