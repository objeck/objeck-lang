#~~
# CSV library
# Copyright (c) 2016-2020 Randy Hollines
~~#
use Collection;

#~
Process and perform calculations on CSV files (-lib csv.obl)
~#
bundle Data.CSV {
	#~
	CSV row

```
csv := CsvTable->New(FileReader->ReadFile("data.csv"));
row := csv->Get(0);
row->Get(0)->PrintLine();       # by index
row->Get("name")->PrintLine();  # by column name
row->Size()->PrintLine();       # column count
```
	~#
	class CsvRow {
		@table : CsvTable;
		@id : Int;
		@columns : CompareVector<String>;
		@read_only : Bool;
		@replace_str : static : String;
		
		#~
		Constructor
		@param table table that has row
		@param id row id
		@param read_only true if readonly, false otherwise
		@param columns column values
		~#
		New(table : CsvTable, id : Int, read_only : Bool, columns : CompareVector<String>) {
			@table := table;
			@id := id;
			@read_only := read_only;
			@columns := columns;

			if(@replace_str = Nil) {
				@replace_str := table->GetReplaceStr();
			};
		}
		
		#~
		Gets the row ID
		@return id
		~#
		method : public : Id() ~ Int {
			return @id;
		}

		#~
		Gets the indexed row value
		@param index index
		@return row value
		~#
		method : public : Get(index : Int) ~ String {
			if(index < @columns->Size()) {
				return @columns->Get(index);
			};
			
			return Nil;
		}

		#~
		Gets the indexed row value
		@param name name
		@return row value
		~#
		method : public : Get(name : String) ~ String {
			index := @table->GetRowId(name);
			if(index < @columns->Size()) {
				return @columns->Get(index);
			};
			
			return Nil;
		}
		
		#~
		Sets the indexed row value
		@param value value to set
		@param index index
		@return true of value was set, false otherwise
		~#
		method : public : Set(index : Int, value : String) ~ Bool {
			if(@read_only = false & index < @columns->Size()) {
				return @columns->Set(value->ReplaceAll("\"", @replace_str), index);
			};
			
			return false;
		}

		#~
		Sets the named row value
		@param name name
		@param value value
		@return true of value was set, false otherwise
		~#
		method : public : Set(name : String, value : String) ~ Bool {
			return Set(@table->GetRowId(name), value);
		}
		
		#~
		Appends value to the end of the row
		@param value to append
		~#
		method : public : Append(value : String) ~ Nil {
			@columns->AddBack(value);
		}
		
		#~
		Gets the row size
		@return row size
		~#
		method : public : Size() ~ Int {
			return @columns->Size();
		}
		
		#~
		Calculates the row sum
		@return row sum
		~#
		method : public : Sum() ~ Float {
			return Sum(0, @columns->Size());
		}

		#~
		Calculates the row sum
		@param end ending column
		@return row sum
		~#
		method : public : Sum(end : Int) ~ Float {
			return Sum(0, end);
		}

		#~
		Calculates the row sum
		@param start starting column
		@param end ending column
		@return row sum
		~#
		method : public : Sum(start : Int, end : Int) ~ Float {
			sum := 0.0;
			
			length := end - start;
			offset := start + length;

# "length={$length}, offset={$offset}"->PrintLine();

			if(length > 0 & offset <= @columns->Size()) {
				for(i := start; i < offset; i += 1;) {
					sum += @columns->Get(i)->ToFloat();
				};
			};
			
			return sum;
		}
		
		#~
		Calculates the row average
		@return row average
		~#
		method : public : Average() ~ Float {
			return Average(0, @columns->Size());
		}

		#~
		Calculates the row average
		@param end ending column
		@return row average
		~#
		method : public : Average(end : Int) ~ Float {
			return Average(0, end);
		}

		#~
		Calculates the row average
		@param start starting column
		@param end ending column
		@return row average
		~#
		method : public : Average(start : Int, end : Int) ~ Float {
			if(@columns->Size() = 0) {
				return 0.0;
			};

			return Sum(start, end) / (end - start + 1);
		}
		
		#~
		Calculates the row median 
		@return row median
		~#
		method : public : Median() ~ Float {
			if(@columns->Size() = 1) {
				@columns->Get(0)->ToFloat();
			};
			
			# store values and sort 
			sorted := CompareVector->New()<FloatRef>;
			each(i : @columns) {
				sorted->AddBack(@columns->Get(i)->ToFloat());
			};
			sorted->Sort();
			
			# find median
			size := sorted->Size();
			if(size % 2 = 0) {
				right := sorted->Get(size / 2);
				left := sorted->Get(right - 1);
				return (left + right) / 2;
			}
			else {
				return sorted->Get(size / 2);
			};
		}
		
		#~
		Formats the row into a string
		@return string representation of the row
		~#
		method : public : ToString() ~ String {
			buffer := "";
			
			each(i : @columns) {
				buffer->Append("\"");
				buffer->Append(@columns->Get(i)->ReplaceAll(@replace_str, "\""));
				if(i + 1 < @columns->Size()) {
					buffer->Append("\",");
				}
				else {			
					buffer->Append("\"");
				};
			};
			
			return buffer;
		}

		#~
		Formats the row into a JSON object string
		@return JSON object string
		~#
		method : public : ToJson() ~ String {
			headers := @table->GetHeaders();
			
			buffer := "{";
			each(i : headers) {
				header := headers->Get(i);
				value := @columns->Get(i);

				buffer->Append('"');
				buffer->Append(header);
				buffer->Append("\":");

				if(value->IsInt() | value->IsFloat()) {
					buffer->Append(value);
				}
				else {
					buffer->Append('"');
					buffer->Append(value);
					buffer->Append('"');
				};

				if(i + 1 < headers->Size()) {
					buffer->Append(',');
				};
			};
			buffer->Append('}');
			
			return buffer;
		}
	}
	
	#~
	CSV column
	~#
	class CsvColumn {
		@rows : Vector<String>;
		
		#~
		Constructor
		@param rows row values
		~#
		New(rows : Vector<String>) {
			@rows := rows;
		}
		
		#~
		Gets the row size
		@return row size
		~#
		method : public : Size() ~ Int {
			return @rows->Size();
		}
		
		#~
		Gets the indexed row value
		@param index index
		@return row value
		~#
		method : public : Get(index : Int) ~ String {
			return @rows->Get(index);
		}
		
		#~
		Calculates the column sum
		@return column sum
		~#
		method : public : Sum() ~ Float {
			return Sum(0, @rows->Size());
		}

		#~
		Calculates the row sum
		@param end ending column
		@return row sum
		~#
		method : public : Sum(end : Int) ~ Float {
			return Sum(0, end);
		}

		#~
		Calculates the row sum
		@param start starting column
		@param end ending column
		@return row sum
		~#
		method : public : Sum(start : Int, end : Int) ~ Float {
			sum := 0.0;
			
			length := end - start;
			offset := start + length;

# "length={$length}, offset={$offset}"->PrintLine();

			if(length > 0 & offset <= @rows->Size()) {
				for(i := start; i < offset; i += 1;) {
					sum += @rows->Get(i)->ToFloat();
				};
			};
				
			return sum;
		}

		#~
		Calculates the column average
		@return column average
		~#
		method : public : Average() ~ Float {
			return Average(0, @rows->Size());
		}

		#~
		Calculates the column average
		@param end ending column
		@return column average
		~#
		method : public : Average(end : Int) ~ Float {
			return Average(0, end);
		}

		#~
		Calculates the column average
		@param start starting column
		@param end ending column
		@return column average
		~#
		method : public : Average(start : Int, end : Int) ~ Float {
			if(@rows->Size() = 0) {
				return 0.0;
			};

			return Sum(start, end) / (end - start + 1);
		}
		
		#~
		Calculates the column median 
		@return column median
		~#
		method : public : Median() ~ Float {
			if(@rows->Size() = 1) {
				@rows->Get(0)->ToFloat();
			};
			
			# store values and sort 
			sorted := CompareVector->New()<FloatRef>;
			each(i : @rows) {
				sorted->AddBack(@rows->Get(i)->ToFloat());
			};
			sorted->Sort();
			
			# find median
			size := sorted->Size();
			if(size % 2 = 0) {
				right := sorted->Get(size / 2);
				left := sorted->Get(right - 1);
				return (left + right) / 2;
			}
			else {
				return sorted->Get(size / 2);
			};
		}
	}
	
	#~
	CSV table

```
csv := CsvTable->New(FileReader->ReadFile("data.csv"));
if(csv->IsParsed()) {
   each(i : csv) {
      row := csv->Get(i);
      row->Get(0)->PrintLine();
   };
};
```
	~#
	class CsvTable {
		@data : Vector<CsvRow>;
		@is_parsed : Bool;
		@header_names : Hash<String, IntRef>;
		@ending : String;
		@replace_str : static : String;

		#~
		Constructor
		@param data CSV data with CRNL line endings
		~#
		New(data : String) {
			Init(data, "\r\n");
		}

		#~
		Constructor
		@param data CSV data
		@param ending line ending
		~#
		New(data : String, ending : String) {
			Init(data, ending);
		}

		method : public : GetReplaceStr() ~ String {
			return @replace_str;
		}
		
		method : Init(data : String, ending : String) ~ Nil {
			if(@replace_str = Nil) {
				@replace_str := "";
				@replace_str += 0xfffd->As(Char);
			};

			@ending := ending;
			@is_parsed := true;
			rows := data->Split(@ending);
			if(rows->Size() > 1) {			
				@data := ParseColumns(rows);
				if(@data = Nil) {
					@is_parsed := false;
				};
			}
			else {
				@is_parsed := false;
			};
			
			if(@is_parsed) {
				@header_names := Hash->New()<String, IntRef>;
				headers := @data->Get(0)->As(CsvRow);
				each(i : headers) {
					@header_names->Insert(headers->Get(i), i);
				};
			};
		}
		
		New(data : Vector<CsvRow>, header_names : Hash<String, IntRef>) {
			@is_parsed := true;
			@data := data;
			@header_names := header_names;
		}
		
		#~
		Returns rather the file has been successfully parsed
		@return true if successfully parsed, false otherwise
		~#
		method : public : IsParsed() ~ Bool {
			return @is_parsed;
		}
		
		#~
		Gets row name
		@param name name
		@return row index
		~#
		method : public : GetRowId(name : String) ~ Int {
			index := @header_names->Find(name);
			if(index <> Nil) {
				return index->Get();
			};
			
			return -1;
		}

		#~
		Gets header names
		@return header names
		~#
		method : public : GetHeaders() ~ CsvRow {
			if(@is_parsed) {
				return @data->Get(0)->As(CsvRow);
			};
			
			return Nil;
		}

		#~
		Appends a column to the end of the table
		@param name name of column to add
		@return ture if column was added, false otherwise
		~#
		method : public : AppendColumn(name : String) ~ Bool {
			if(@header_names->Has(name) = false & @is_parsed) {
				@data->Get(0)->As(CsvRow)->Append(name);
				for(i := 1; i < @data->Size(); i+=1;) {
					@data->Get(i)->As(CsvRow)->Append("");
				};
				@header_names->Insert(name, RowSize() - 1);
				
				return true;
			};
			
			return false;
		}
		
		#~
		Removes a row from the table
		@param id row to delete
		@return true if deleted false otherwise
		~#
		method : public : Delete(id : Int) ~ Bool {
			if(id = 0) {
				return false;
			};
			
			return @data->Remove(id) <> Nil;
		}
		
		#~
		Gets the size of rows
		@return row size
		~#
		method : public : RowSize() ~ Int {
			if(<>@is_parsed) {
				return 0;
			};
			
			return @data->Get(0)->As(CsvRow)->Size();
		}
		
		method : native : ParseColumns(rows : String[]) ~ Vector<CsvRow> {
			parsed_rows := Vector->New()<CsvRow>;
			each(i : rows) {
				parsed_rows->AddBack(CsvRow->New(@self, i, i = 0, ParseRow(rows[i]->ReplaceAll("\"\"", @replace_str))));
			};

			if(rows->Size() <> parsed_rows->Size()) {		
# IO.Console->Print("rows: parsed = ")->Print(rows->Size())->Print(", size = ")->PrintLine(parsed_rows->Size());				
				return Nil;
			};
			
			column_size := parsed_rows->Get(0)->Size();
			each(i : parsed_rows) {
				parsed_row := parsed_rows->Get(i)->As(CsvRow);
				if(parsed_row->Size() < column_size) {
# IO.Console->Print("columns: parsed = ")->Print(parsed_row->Size())->Print(", size = ")->PrintLine(column_size);
					return Nil;
				};
			};
			
			return parsed_rows;
		}
		
		method : native : ParseRow(row : String) ~ CompareVector<String> {
			columns := CompareVector->New()<String>;
			
			index := 0;
			while(index < row->Size()) {
				# stripe white space
				if(index < row->Size()) {
	                char := row->Get(index);
					while(index < row->Size() & (char = ' ' | char = '\t' | char = '\f' | char = '\v')) {
						char := row->Get(++index);
					};
				};

				if(row->Get(index) = '"') {
					index += 1;
					start := index;
					while(index < row->Size() & row->Get(index) <> '"') {
						index += 1;
					};
					token := row->SubString(start, index - start);
					index += 1;
					if(token <> Nil) {
						columns->AddBack(token->ReplaceAll(@replace_str, "\""));
					};
# IO.Console->Print("quoted = |")->Print(token->ReplaceAll(@replace_str, "\""))->PrintLine("|");
				}
				else {
					start := index;
					while(index < row->Size() & row->Get(index) <> ',') {
						index += 1;
					};

					token := row->SubString(start, index - start);
					if(token = Nil | token->Equals(@replace_str)) {
						columns->AddBack("");
# "normal = ||"->PrintLine();
					}
					else {
						columns->AddBack(token->ReplaceAll(@replace_str, "\"")->Trim());
# IO.Console->Print("normal = |")->Print(token->ReplaceAll(@replace_str, "\"")->Trim())->PrintLine("|");											
					};
				};
			
				if(row->Get(index) = ',') {
					index += 1;
					if(index = row->Size()) {
						columns->AddBack("");
# "normal = ||"->PrintLine();						
					};
				};
			};
					
			return columns;
		}
		
		#~
		Gets an indexed row
		@param index index
		@return row
		~#
		method : public : Get(index : Int) ~ CsvRow {
			if(<>@is_parsed) {
				return Nil;
			};
			
			return @data->Get(index)->As(CsvRow);
		}
		
		#~
		Gets the number of rows
		@return number of rows
		~#
		method : public : Size() ~ Int {
			if(<>@is_parsed) {
				return 0;
			};

			return @data->Size();
		}
				
		#~
		Searches a given column for matching values
		@param name column name
		@param value value to search for
		@return table of matching results 
		~#
		method : public : Matches(name : String, value : String) ~ CsvTable {
			index := @header_names->Find(name);
			if(index <> Nil) {
				return Matches(index->Get(), value);
			};
			
			return Nil;
		}
		
		#~
		Searches a given column for matching values
		@param index column index
		@param value value to search for
		@return table of matching results 
		~#
		method : public : Matches(index : Int, value : String) ~ CsvTable {
			if(index >= RowSize()) {
				return Nil;
			};
			
			found := Vector->New()<CsvRow>;
			found->AddBack(@data->Get(0));
			
			for(i := 1; i < @data->Size(); i +=1;) {
				row := @data->Get(i);
				if(row->Get(index)->Equals(value)) {
					found->AddBack(row);
				};
			};
			
			return CsvTable->New(found, @header_names);
		}
		
		#~
		Counts a column for matching values
		@param name column name
		@param value value to search for
		@return number of occurrences
		~#
		method : public : CountMatches(name : String, value : String) ~ Int {
			index := @header_names->Find(name);
			if(index <> Nil) {
				return CountMatches(index->Get(), value);
			};
			
			return 0;
		}
		
		#~
		Counts a column for matching values
		@param index column index
		@param value value to search for
		@return number of occurrences
		~#
		method : public : CountMatches(index : Int, value : String) ~ Int {
			if(index >= RowSize()) {
				return 0;
			};
			
			count := 0;
			for(i := 1; i < @data->Size(); i +=1;) {
				row := @data->Get(i)->As(CsvRow);
				if(row->Get(index)->Equals(value)) {
					count += 1;
				};
			};
			
			return count;
		}

		#~
		Searches a given column for like values
		@param name column name
		@param value value to search for
		@return table of like results 
		~#
		method : public : Contains(name : String, value : String) ~ CsvTable {
			index := @header_names->Find(name);
			if(index <> Nil) {
				return Contains(index->Get(), value);
			};
			
			return Nil;
		}
		
		#~
		Searches a given column for like values
		@param index column index
		@param value value to search for
		@return table of like results 
		~#
		method : public : Contains(index : Int, value : String) ~ CsvTable {
			if(index >= RowSize()) {
				return Nil;
			};
			
			found := Vector->New()<CsvRow>;
			found->AddBack(@data->Get(0));
			
			for(i := 1; i < @data->Size(); i +=1;) {
				row := @data->Get(i);
				if(row->Get(index)->Has(value)) {
					found->AddBack(row);
				};
			};
			
			return CsvTable->New(found, @header_names);
		}
		
		#~
		Counts a column for like values
		@param name column name
		@param value value to search for
		@return number of occurrences
		~#
		method : public : CountContains(name : String, value : String) ~ Int {
			index := @header_names->Find(name);
			if(index <> Nil) {
				return CountContains(index->Get(), value);
			};
			
			return 0;
		}
		
		#~
		Counts a column for like values
		@param index column index
		@param value value to search for
		@return number of occurrences
		~#
		method : public : CountContains(index : Int, value : String) ~ Int {
			if(index >= RowSize()) {
				return 0;
			};
			
			count := 0;
			for(i := 1; i < @data->Size(); i +=1;) {
				row := @data->Get(i)->As(CsvRow);
				if(row->Get(index)->Has(value)) {
					count += 1;
				};
			};
			
			return count;
		}

		#~
		Formats the table into a string
		@return string representation of the table
		~#
		method : public : ToString() ~ String {
			buffer := "";

			each(i : @data) {
				row := @data->Get(i)->As(CsvRow);
				buffer->Append(row->ToString());
				buffer->Append(@ending);
			};
		
			return buffer;
		}

		#~
		Formats the table into a JSON object string
		@return JSON object string
		~#
		method : public : ToJson() ~ String {
			buffer := "["
			for(i := 1; i < @data->Size(); i += 1;) {
				row := @data->Get(i);
				buffer->Append(row->ToJson());
				
				if(i + 1 < @data->Size()) {
					buffer->Append(',');
				};
			};
			buffer->Append(']');

			return buffer;
		}
		
		#~
		Get unique values for a given row
		@param name column name
		@return unique values for the given row
		~#
		method : public : UniqueColumnValues(name : String) ~ CsvColumn {
			index := @header_names->Find(name);
			if(index <> Nil) {
				return UniqueColumnValues(index->Get());
			};
			
			return Nil;
		}
		
		#~
		Get values for a given row
		@param index column index
		@return values for the given row
		~#
		method : public : ColumnValues(index : Int) ~ CsvColumn {
			if(index >= RowSize()) {
				return Nil;
			};
			
			values := Vector->New()<String>;
			for(i := 1; i < @data->Size(); i +=1;) {
				value := @data->Get(i)->As(CsvRow)->Get(index);
				values->AddBack(value);
			};
			
			return CsvColumn->New(values);
		}
		
		#~
		Get values for a given row
		@param name column name
		@return values for the given row
		~#
		method : public : ColumnValues(name : String) ~ CsvColumn {
			index := @header_names->Find(name);
			if(index <> Nil) {
				return ColumnValues(index->Get());
			};
			
			return Nil;
		}
		
		#~
		Get unique values for a given row
		@param index column index
		@return unique values for the given row
		~#
		method : public : UniqueColumnValues(index : Int) ~ CsvColumn {
			if(index >= RowSize()) {
				return Nil;
			};
			
			values := Vector->New()<String>;
			uniques := Set->New()<String>;
			for(i := 1; i < @data->Size(); i +=1;) {
				value := @data->Get(i)->As(CsvRow)->Get(index);
				if(<>uniques->Has(value)) {
					values->AddBack(value);
					uniques->Insert(value);
				};
			};
			
			return CsvColumn->New(values);
		}
	}
}
