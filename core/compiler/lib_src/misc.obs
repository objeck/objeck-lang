use Web.HTTP, Collection, Data.JSON, System.Time, System.IO.Filesystem;

#~
Provides support for conversions (-lib misc)
~#	
bundle System.Time {
	#~
	Utilities for handling dates

```
date := DateUtility->Parse("12/25/2024", "MM/dd/yyyy", false);
date->GetMonth()->PrintLine();
date->GetDay()->PrintLine();
date->GetYear()->PrintLine();
```
	~#
	class DateUtility {
		#~
		Parses a date from a String.
		@param date_str date
		@param format date string format <p>options: <ul> <li>month=MM</li><li>day=dd</li><li>year=yy(yy)</li><li>hour=hh</li><li>minutes=mm</li><li>seconds=ss</p><ul>
		@param gmt true is GMT, false otherwise
		@return Date instance
		~#
		function : Parse(date_str : String, format : String, gmt : Bool) ~ Date {
			parts := Map->New()<IntRef, DatePart>;
		
			# month
			month := format->Find("MM");
			if(month > -1) {
				parts->Insert(month, DatePart->New(month, DatePart->Type->Month));
			};

			# day
			day := format->Find("dd");
			if(day > -1) {
				parts->Insert(day, DatePart->New(day, DatePart->Type->Day));
			};

			# year
			long_year_format := true;
			year := format->Find("yyyy");
			if(year > -1) {
				parts->Insert(year, DatePart->New(year, DatePart->Type->Year));
			}
			else {
				year := format->Find("yy");
				if(year > -1) {
					parts->Insert(year, DatePart->New(year, DatePart->Type->Year));
					long_year_format := false;
				};
			};
	
			# hour
			hours := format->Find("hh");
			if(hours > -1) {
				parts->Insert(hours, DatePart->New(hours, DatePart->Type->Hour));
			};

			# mins
			mins := format->Find("mm");
			if(mins > -1) {
				parts->Insert(mins, DatePart->New(mins, DatePart->Type->Min));
			};

			# secs
			secs := format->Find("ss");
			if(secs > -1) {
				parts->Insert(secs, DatePart->New(secs, DatePart->Type->Sec));
			};

			date_month := 0; date_day := 0; date_year := 0;
			date_hours := 0; date_mins := 0; date_secs := 0;
		
			offset := 0;
			values := parts->GetValues()<DatePart>;
			each(i : values) {
				part := values->Get(i);
				type := part->GetType();

				select(type) {
					label DatePart->Type->Month: {
						pos := month + offset + 1;
						if(pos > -1 & pos < date_str->Size()) { 
							if(date_str->Get(pos)->IsDigit()) {
								date_month := date_str->SubString(month + offset, 2)->ToInt();
							}
							else {
								date_month := date_str->SubString(month + offset, 1)->ToInt();
								offset -= 1;
							};
						};
					}

					label DatePart->Type->Day: {
						pos := day + offset + 1;
						if(pos > -1 & pos < date_str->Size()) { 
							if(date_str->Get(pos)->IsDigit()) {
								date_day := date_str->SubString(day + offset, 2)->ToInt();
							}
							else {
								date_day := date_str->SubString(day + offset, 1)->ToInt();
								offset -= 1;
							};
						};
					}
				
					label DatePart->Type->Year: {
						pos := year + offset;
						if(pos > -1 & pos < date_str->Size()) {
							if(long_year_format) {
								date_year := date_str->SubString(year + offset, 4)->ToInt();
							}	
							else {
								date_year := 2000;
								date_year += date_str->SubString(year + offset, 2)->ToInt();
							};
						};
					}

					label DatePart->Type->Hour: {
						pos := hours + offset + 1;
						if(pos > -1 & pos < date_str->Size()) { 
							if(date_str->Get(pos)->IsDigit()) {
								date_hours := date_str->SubString(hours + offset, 2)->ToInt() - 1;
							}
							else {
								date_hours := date_str->SubString(hours + offset, 1)->ToInt() - 1;
								offset -= 1;
							};
						};
					}

					label DatePart->Type->Min: {
						pos := mins + offset + 1;
						if(pos > -1 & pos < date_str->Size()) { 
							if(date_str->Get(pos)->IsDigit()) {
								date_mins := date_str->SubString(mins + offset, 2)->ToInt();
							}
							else {
								date_mins := date_str->SubString(mins + offset, 1)->ToInt();
								offset -= 1;
							};
						};
					}

					label DatePart->Type->Sec: {
						pos := secs + offset + 1;
						if(pos > -1 & pos < date_str->Size()) { 
							if(date_str->Get(pos)->IsDigit()) {
								date_secs := date_str->SubString(secs + offset, 2)->ToInt();
							}
							else {
								date_secs := date_str->SubString(secs + offset, 1)->ToInt();
								offset -= 1;
							};
						};
					}
				};
			};

			date := Date->New(date_day, date_month, date_year, gmt);
			date->AddHours(date_hours);
			date->AddMinutes(date_mins);
			date->AddSeconds(date_secs);

			return date;
		}

		#~
		Formats date into a string
		@param date date 
		@param format date string format <p>options: <ul> <li>month=MM</li><li>day=dd</li><li>year=yy(yy)</li><li>hour=hh</li><li>minutes=mm</li><li>seconds=ss</p><ul>
		@return Date instance
		~#
		function : Format(date : System.Time.Date, format : String) ~ String {
			day := date->GetDay();
			hours := date->GetHours();
			mins := date->GetMinutes();
			mon := date->GetMonth();
			secs := date->GetSeconds();
			year := date->GetYear();

			buffer := "";

			for(i := 0; i < format->Size(); i += 1;) {
				cur_char := format->Get(i);
				next_char := '\0';
				if(i + 1 < format->Size()) {
					next_char := format->Get(i + 1);
				};

				# day
				if(cur_char = 'd' & next_char = 'd') {
					buffer->Append(day);
					i += 1;
				}
				# month
				else if(cur_char = 'M' & next_char = 'M') {
					buffer->Append(mon);
					i += 1;
				}
				# year
				else if(cur_char = 'y' & next_char = 'y') {
					if(i + 3 < format->Size() & format->Get(i + 2) = 'y' & format->Get(i + 3) = 'y') {
						next_char := format->Get(i + 1);
						i += 3;
						buffer->Append(year);
					}
					else {
						i += 1;
						buffer->Append(year - 2000);
					};
				}
				# hours
				else if(cur_char = 'h' & next_char = 'h') {
					buffer->Append(hours);
					i += 1;
				}
				# minutes
				else if(cur_char = 'm' & next_char = 'm') {
					buffer->Append(mins);
					i += 1;
				}
				# seconds
				else if(cur_char = 's' & next_char = 's') {
					buffer->Append(secs);
					i += 1;
				}
				# other
				else {
					buffer->Append(cur_char);
				}
			};

			return buffer;
		}

		#~
		Get the network time
		@return triplet with: unix time, UTC time and local time
		~#
		function : GetNetworkTime() ~ Collection.Tuple.Triplet<IntRef, Date, Date> {
			response := HttpClient->QuickGet(Url->New("http://worldtimeapi.org/api/ip"));
			if(response->GetCode() = 200) {
				response_json := JsonParser->TextToElement(response->GetContent()->ToString());
				if(response_json <> Nil) {
					unix_time_json := response_json->Get("unixtime");					
					utc_time_json := response_json->Get("utc_datetime");
					dst_time_json := response_json->Get("datetime");
					if(unix_time_json <> Nil & utc_time_json <> Nil & dst_time_json <> Nil) {
						utc_date := System.Time.DateUtility->Parse(utc_time_json->GetString(), "yyyy-MM-ddThh:mm:ss", false);
						dst_date := System.Time.DateUtility->Parse(dst_time_json->GetString(), "yyyy-MM-ddThh:mm:ss", false);	
						
						return Collection.Tuple.Triplet->New(IntRef->New(unix_time_json->GetInt()), utc_date, dst_date)<IntRef, Date, Date>;
					};
				};
			};

			return Nil;
		}
	}
	
	class : private : DatePart {
		@index : Int;
		@type : Type;

		enum Type {
			Month,
			Day,
			Year,
			Hour,
			Min,
			Sec
		}

		New(index : Int, type : Type) {
			@index := index;
			@type := type;
		}

		method : public : GetType() ~ Type {
			return @type;
		}

		method : public : GetIndex() ~ Int {
			return @index;
		}
	}
}

#~
Various support utilities (-lib misc)
~#	
bundle System.Utility {
	#~
	Utilities for parsing
	~#
	class Parser {
		#~
		Simple tokenizer 
		@param stream_in source input
		@return vector of string tokens
		~#
		function : Tokenize(stream_in : String) ~ Vector<String> {
			tokens := Vector->New()<String>;

			each(i : stream_in) {
				# identifier
				if(stream_in->Get(i)->IsChar()) {
					start := i;
					while(stream_in->Get(i)->IsChar() | stream_in->Get(i)->IsDigit() | stream_in->Get(i) = '_') {
						i += 1;
					};
					tokens->AddBack(stream_in->SubString(start, i - start));
					i -= 1;
				}
				# number
				else if(stream_in->Get(i)->IsDigit()) {
					start := i;
					while(stream_in->Get(i)->IsDigit() | stream_in->Get(i) = '.') {
						i += 1;
					};
					tokens->AddBack(stream_in->SubString(start, i - start));
					i -= 1;
				}
				# other
				else {
					value := stream_in->Get(i);
					if(value <> ' ' & value <> '\t' & value <> '\r' & value <> '\n') {
						tokens->AddBack(value->ToString());
					};
				};
			};

			return tokens;
		}

		#~
		Command line parser
		@param args command line arguments
		@return name/value pairs
		~#
		function : CommandParameters(args : String[]) ~ Hash<String, String> {
			params := Hash->New()<String, String>;

			each(arg := args) {
				name := "";
				value := "";

				index := arg->Find('=');
				if(index > 0) {
					name += arg->SubString(index);
					index += 1;
					value += arg->SubString(index, arg->Size() - index);
				}
				else {
					name := arg;
				};

				params->Insert(name, value);
			};

			return params;
		}
	}

	#~
	Console spinner
	~#
	class Spinner {
		@state : static : Int;
		@count : static : Int;

		#~
		Prints and advances the spinner to the next state
		~#
		function : Next() ~ Nil {
			select(@state) {
				label 0 {
					"⋅\r"->Print();
					@state += 1;
				}

				label 1 {
					"•\r"->Print();
					@state += 1;
				}

				label 2 {
					"●\r"->Print();
					@state += 1;
				}

				label 3 {
					"⬤\r"->Print();
					@state := 0;
				}
			};

			@count += 1;
			Console->Flush();
		}

		#~
		Prints the final state
		~#
		function : Last() ~ Nil {
			"\r·"->Print();
			Console->Flush();
		}
	}

	#~
	Code formatter
	~#
	class CodeFormatter {
		@token : Token;
		@prev_token : Token;
		@next_token : Token;
		@skip_space : Bool;

		@stmt_space : Bool;
		@ident_space : Int;

		@buffer : String;
		@start_range : Int;
		@end_range : Int;

		@trim_trailing : Bool;
		@is_generic : Bool;

		ASCII_COLOR_TYPE : String;
		ASCII_COLOR_KEYWORD : String;
		ASCII_COLOR_OPER : String;
		ASCII_COLOR_CONST : String;
		ASCII_COLOR_BRACKET : String;
		ASCII_COLOR_COMMENT : String;
		ASCII_END : String;

		#~
		Constructor
		@param options options
	
		<p>Support for following options:<p>
		<ul>
		  <li>"function-space" - (true|false)</li>
		  <li>"ident-space" - (number|0 for tabs)</li>
		  <li>"trim-trailing" - (true|false)</li>
		  <li>"start-line" - (optional|number)</li>
		  <li>"end-line" - (optional|number)</li>
		</ul>		
		~#
		New(options : Map<String, String>) {
			ASCII_COLOR_KEYWORD := "\x1B[33m";
			ASCII_COLOR_TYPE := "\x1B[94m";
			ASCII_COLOR_OPER := "\x1B[32m";
			ASCII_COLOR_CONST := "\x1B[36m";
			ASCII_COLOR_BRACKET := "\x1B[31m";
			ASCII_COLOR_COMMENT := "\x1B[90m";
			ASCII_END := "\x1b[0m";

			# get options
			stmt_space_check := options->Find("function-space");
			@stmt_space := stmt_space_check <> Nil & stmt_space_check->Equals("true");

			# trim trailing
			trim_trailing_check := options->Find("trim-trailing");
			@trim_trailing := trim_trailing_check <> Nil & trim_trailing_check->Equals("true");

			@ident_space := 0;
			ident_space_check := options->Find("ident-space");
			if(ident_space_check <> Nil & <>ident_space_check->IsEmpty()) {
				@ident_space := ident_space_check->ToInt() - 1;
			};
			
			# get start and end range
			@start_range := -1;
			start_line_check := options->Find("start-line");
			if(start_line_check <> Nil & <>start_line_check->IsEmpty()) {
				@start_range := start_line_check->ToInt();
			};

			@end_range := -1;
			end_line_check := options->Find("end-line");
			if(end_line_check <> Nil & <>end_line_check->IsEmpty()) {
				@end_range := end_line_check->ToInt();
			};


			@buffer := "";
		}

		method : AppendBuffer(str : String) ~ Nil {
			if(InRange()) {
				if(@buffer->EndsWith("; ") | @buffer->EndsWith("} ")) {
					@buffer->Pop();
				};

				@buffer += str;
			};
		}

		method : AppendBuffer(char : Char) ~ Nil {
			if(InRange()) {
				if(@buffer->EndsWith("; ") | @buffer->EndsWith("} ")) {
					@buffer->Pop();
				};

				@buffer += char;
			};
		}

		method : PopBuffer() ~ Nil {
			if(InRange()) {
				@buffer->Pop();
			};
		}

		method : InRange() ~ Bool {
			return (@start_range < 0 & @end_range < 0) | (@token <> Nil & @start_range <= @token->GetLine() & @end_range >= @token->GetLine());
		}	

		method : CheckGeneric(tokens : Vector<Token>, i : Int) ~ Bool {
			level := 0;
			
			token := tokens->Get(i);
			while(token->GetType() <> System.Utility.Token->Type->SEMI & token->GetType() <> System.Utility.Token->Type->OTHER) {
				select(token->GetType()) {
					label System.Utility.Token->Type->LESS {
						level += 1;
					}

					label System.Utility.Token->Type->GTR {
						level -= 1;
					}

					label System.Utility.Token->Type->IDENT
					label System.Utility.Token->Type->STRING_ID
					label System.Utility.Token->Type->COMMA {
						# skip
					}

					other {
						return false;
					}
				}

				if(level = 0) {
					return true;
				};

				token := tokens->Get(++i);
			}

			return false;
		}

		#~
		@param source source code
		@param is_color true for ascii color formtting, false otherwise
		@return formatter code
		~#
		method : public : native : Format(source : String, is_color : Bool) ~ String {
			scanner := Scanner->New(source);
			if(scanner->Scan()) {
				tab_space := 0;
				insert_tabs := false;

				@skip_space := false;
				in_label := false;
				in_for := false;
				in_consts := false;

				@prev_token := Token->New(-1, System.Utility.Token->Type->OTHER, "other");

				tokens := scanner->GetTokens();
				each(i : tokens) {
					if(insert_tabs) {
						TabSpace(tab_space, @ident_space);
						insert_tabs := false;
					};

					# set current, previous and next tokens
					if(@token <> Nil) {
						@prev_token := @token;
					};

					if(@next_token <> Nil) {
						@token := @next_token;
					}
					else {
						@token := tokens->Get(i);
					};

					if(i + 1 < tokens->Size()) {
						@next_token := tokens->Get(i + 1);
					}
					else {
						@next_token := Token->New(-1, System.Utility.Token->Type->OTHER, "other");
					};
					
					select(@token->GetType()) {
						label System.Utility.Token->Type->CLASS_ID {
							VerticalSpace(@prev_token, tab_space, @ident_space);
							
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};

							AppendBuffer("class");

							if(is_color) {
								AppendBuffer(ASCII_END);
							}
						}

						label System.Utility.Token->Type->FUNCTION_ID {
							VerticalSpace(@prev_token, tab_space, @ident_space);

							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("function");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->METHOD_ID {
							VerticalSpace(@prev_token, tab_space, @ident_space);
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};

							AppendBuffer("method");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->PUBLIC_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};

							AppendBuffer("public");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->PRIVATE_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};

							AppendBuffer("private");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->IF_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("if");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->ELSE_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer('\n');
							TabSpace(tab_space, @ident_space);
							AppendBuffer("else");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}					

						label System.Utility.Token->Type->DO_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("do");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->WHILE_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("while");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->FOR_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("for");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};

							in_for := true;
						}

						label System.Utility.Token->Type->EACH_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("each");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->BREAK_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("break");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->CONTINUE_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("continue");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->RETURN_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("return");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->ALIAS_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("alias");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->LEAVING_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("leaving");
						}

						label System.Utility.Token->Type->USE_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("use");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->NATIVE_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("native");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->STATIC_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("static");
						}

						label System.Utility.Token->Type->SELECT_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("select");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->LABEL_ID {
							VerticalSpace(@prev_token, tab_space, @ident_space);

							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};

							AppendBuffer("label");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};

							in_label := true;
							@skip_space := false;
						}

						label System.Utility.Token->Type->OTHER_ID {
							VerticalSpace(@prev_token, tab_space, @ident_space);
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("other");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};

							in_label := true;
							@skip_space := false;
						}

						label System.Utility.Token->Type->ASGN {
							AppendBuffer(":=");
						}

						label System.Utility.Token->Type->ASSESSOR {
							PopBuffer();
							AppendBuffer("->");
						}

						label System.Utility.Token->Type->LINE_COMMENT {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_COMMENT);
							};

							AppendBuffer("# ");
							AppendBuffer(@token->GetValue()->Trim());
							
							if(is_color) {
								AppendBuffer("\033[0m");
							};
							
							AppendBuffer('\n');
							@skip_space := insert_tabs := true;
						}

						label System.Utility.Token->Type->MULTI_COMMENT {
							PopBuffer();
							
							if(is_color) {
								AppendBuffer(ASCII_COLOR_COMMENT);
							};

							comment := @token->GetValue()->Trim();

							AppendBuffer(" #~\n");
							each(k : @ident_space) {
								AppendBuffer(' ');
							};
							AppendBuffer(' ');

							words := System.Utility.Parser->Tokenize(comment);
							each(word := words) {
								if(word->StartsWith('@')) {
									AppendBuffer('\n');
									each(k : @ident_space) {
										AppendBuffer(' ');
									};
									AppendBuffer(' ');
									AppendBuffer(word);
								}
								else {
									AppendBuffer(word);
									if(word->StartsWith(';')) {
										AppendBuffer('\n');
										each(k : @ident_space) {
											AppendBuffer(' ');
										};
									}
									AppendBuffer(' ');
								}
							};

							AppendBuffer('\n');
							each(k : @ident_space) {
								AppendBuffer(' ');
							};
							AppendBuffer(" ~#");

							AppendBuffer('\n');
							each(k : @ident_space) {
								AppendBuffer(' ');
							};

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->SEMI {
							PopBuffer();
							AppendBuffer(';');

							if(@next_token->GetType() <> System.Utility.Token->Type->CCBRACE & @next_token->GetType() <> System.Utility.Token->Type->VTAB & <>in_for) {
								AppendBuffer('\n');
								
								@skip_space := true;
								if(<>in_for) {
									insert_tabs := true;
								};
							};
						}

						label System.Utility.Token->Type->LESS {
							@is_generic := CheckGeneric(tokens, i);

							if(@is_generic) {
								PopBuffer();
							};

							AppendBuffer('<');

							if(@is_generic) {
								@skip_space := true;
							};
						}

						label System.Utility.Token->Type->GTR {
							if(@is_generic) {
								PopBuffer();
								@is_generic := false;
							};

							AppendBuffer('>');
						}

						label System.Utility.Token->Type->COMMA {
							PopBuffer();
							AppendBuffer(',');

							if(in_consts) {
								AppendBuffer('\n');
								insert_tabs := @skip_space := true;
							};
						}

						label System.Utility.Token->Type->NEQL {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("<>");

							if(is_color) {
								AppendBuffer(ASCII_END);
							}
						}

						label System.Utility.Token->Type->AND {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer('&');

							if(is_color) {
								AppendBuffer(ASCII_END);
							}
						}

						label System.Utility.Token->Type->OR {
							AppendBuffer('|');
						}

						label System.Utility.Token->Type->QUESTION {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer('?');

							if(is_color) {
								AppendBuffer(ASCII_END);
							}
						}

						label System.Utility.Token->Type->IDENT {
							if(@prev_token->GetType() = System.Utility.Token->Type->ASSESSOR | 
									@prev_token->GetType() = System.Utility.Token->Type->ADD_ADD | 
									@prev_token->GetType() = System.Utility.Token->Type->SUB_SUB) {
								PopBuffer();
							};
							value := @token->GetValue();
							AppendBuffer("{$value}");
						}

						label System.Utility.Token->Type->STRING_LIT {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_CONST);
							}
							
							value := @token->GetValue();
							AppendBuffer('"');
							AppendBuffer(value);
							AppendBuffer('"');

							if(is_color) {
								AppendBuffer(ASCII_END);
							}
						}

						label System.Utility.Token->Type->CHAR_LIT {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_CONST);
							}

							AppendBuffer('\'');
							AppendBuffer(@token->GetValue());
							AppendBuffer('\'');

							if(is_color) {
								AppendBuffer(ASCII_END);
							}
						}

						label System.Utility.Token->Type->NUM_LIT {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_CONST);
							}

							AppendBuffer(@token->GetValue());

							if(is_color) {
								AppendBuffer(ASCII_END);
							}
						}
						
						label System.Utility.Token->Type->STRING_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_TYPE);
							};

							AppendBuffer("String");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->INT_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_TYPE);
							};

							AppendBuffer("Int");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->FLOAT_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_TYPE);
							};
							
							AppendBuffer("Float");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->CHAR_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_TYPE);
							};

							AppendBuffer("Char");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->BOOL_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_TYPE);
							};

							AppendBuffer("Bool");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->BYTE_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_TYPE);
							};

							AppendBuffer("Byte");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->NIL_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_TYPE);
							};

							AppendBuffer("Nil");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->AND_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("and");

							if(is_color) {
								AppendBuffer(ASCII_END);
							}
						}

						label System.Utility.Token->Type->OR_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};
							
							AppendBuffer("or");

							if(is_color) {
								AppendBuffer(ASCII_END);
							}
						}

						label System.Utility.Token->Type->XOR_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};

							AppendBuffer("xor");

							if(is_color) {
								AppendBuffer(ASCII_END);
							}
						}

						label System.Utility.Token->Type->VIRTUAL_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};

							AppendBuffer("virtual");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->BUNDLE_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};

							AppendBuffer("bundle");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->INTERFACE_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};

							AppendBuffer("interface");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->IMPLEMENTS_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};

							AppendBuffer("implements");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->ENUM_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};

							AppendBuffer("enum");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->CONSTS_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};

							in_consts := true;
							AppendBuffer("consts");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->REVERSE_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};

							AppendBuffer("reverse");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->PARENT_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};

							AppendBuffer("parent");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->FROM_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_KEYWORD);
							};

							AppendBuffer("from");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->TRUE_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_CONST);
							};

							AppendBuffer("true");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->FALSE_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_CONST);
							};

							AppendBuffer("false");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->NEW_ID {
							PopBuffer();
							@skip_space := true;
							AppendBuffer("New");
						}

						label System.Utility.Token->Type->AS_ID {
							PopBuffer();
							@skip_space := true;
							AppendBuffer("As");
						}

						label System.Utility.Token->Type->TYPE_OF_ID {
							PopBuffer();
							@skip_space := true;
							AppendBuffer("TypeOf");
						}

						label System.Utility.Token->Type->CRITICAL_ID {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_CONST);
							};

							AppendBuffer("critical");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->COLON {
							if(in_label) {
								PopBuffer();
							};
							AppendBuffer(':');
						}

						label System.Utility.Token->Type->ADD {
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer('+');

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->SUB{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer('-');

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->MUL{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer('*');

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->DIV{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer('/');

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->MOD{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};
							AppendBuffer('%');
						}

						label System.Utility.Token->Type->QUOTE{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer('\'');

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->FWD_SLASH{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer('\\');

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->EQL{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer('=');

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->LESS_EQL{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer("<=");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->ESCAPE{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer("\e");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}


						label System.Utility.Token->Type->GTR_EQL{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer(">=");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->ADD_ASN{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer("+=");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->SUB_ASN{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer("-=");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->ADD_ADD{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer("++");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->SUB_SUB{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer("--");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->MUL_ASN{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer("*=");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->DIV_ASN{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer("/=");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->LAMBDA{
							if(is_color) {
								AppendBuffer(ASCII_COLOR_OPER);
							};

							AppendBuffer("=>");

							if(is_color) {
								AppendBuffer(ASCII_END);
							};
						}

						label System.Utility.Token->Type->TILDE{
							AppendBuffer('~');
						}

						label System.Utility.Token->Type->OBRACE {
							if(@prev_token->GetType() = System.Utility.Token->Type->IDENT) {
								PopBuffer();
							};

							AppendBuffer('[');
							@skip_space := true;
						}

						label System.Utility.Token->Type->CBRACE {
							if(@prev_token->GetType() <> System.Utility.Token->Type->OBRACE) {
								PopBuffer();
							};
							AppendBuffer(']');
						}

						label System.Utility.Token->Type->OPREN {
							if(<>@stmt_space) {
								select(@prev_token->GetType()) {
									label System.Utility.Token->Type->IDENT
									label System.Utility.Token->Type->IF_ID
									label System.Utility.Token->Type->FOR_ID
									label System.Utility.Token->Type->EACH_ID
									label System.Utility.Token->Type->SELECT_ID
									label System.Utility.Token->Type->WHILE_ID {
										PopBuffer();
									}
								};
							};

							AppendBuffer('(');
							@skip_space := true;
						}

						label System.Utility.Token->Type->CPREN {
							if(@prev_token->GetType() <> System.Utility.Token->Type->OPREN) {
								PopBuffer();
							};

							if(in_for) {
								in_for := false;
							};

							AppendBuffer(')');
						}

						label System.Utility.Token->Type->OCBRACE {
							AppendBuffer("{\n");
							@skip_space := true;
							tab_space += 1;
							insert_tabs := true;

							if(in_label) {
								in_label := false;
							};
						}

						label System.Utility.Token->Type->CCBRACE {
							AppendBuffer('\n');
							tab_space -= 1;
							TabSpace(tab_space, @ident_space);
							AppendBuffer('}'); # TODO: Fix me!

							if(in_consts) {
								in_consts := false;
							};
						}

						label System.Utility.Token->Type->VTAB {
							if(@next_token->GetType() <> System.Utility.Token->Type->CCBRACE) {
								AppendBuffer("\n\n");
								TabSpace(tab_space, @ident_space);
								@skip_space := true;
							};
						}

						other {
							"--- OTHER ---"->ErrorLine();
						}
					};

					if(<>@skip_space) {
						AppendBuffer(' ');
					}
					else {
						@skip_space := false;
					};
				};
			};
			
			# clean up output
			offset_start := 0;
			each(i : @buffer) {
				if(@buffer->Get(i) = '\n' | @buffer->Get(i) = '\r') {
					offset_start += 1;
				}
				else {
					break;
				};
			};

			offset_end := 0;
			reverse(i : @buffer) {
				if(@buffer->Get(i) = ' ' | @buffer->Get(i) = '\t') {
					offset_end += 1;
				}
				else {
					break;
				};
			};

			formatted := @buffer->SubString(offset_start, @buffer->Size() - offset_start - offset_end);
			
			if(@trim_trailing & formatted <> Nil) {
				formatted := formatted->Trim();
			};
			
			return formatted;
		}

		method : VerticalSpace(@prev_token : Token, tab_space : Int, @ident_space : Int) ~ Nil {
			if(@prev_token->GetType() = System.Utility.Token->Type->CCBRACE) {
				AppendBuffer("\n\n");
				TabSpace(tab_space, @ident_space);
				@skip_space := true;
			};
		}

		method : TabSpace(tab_space : Int, F : Int) ~ Nil {
			if(tab_space > 0) {
				each(i : tab_space) {
					each(j : @ident_space) {
						AppendBuffer(' ');
					};
					AppendBuffer(' ');
				};
			}
			else {
				each(i : tab_space) {
					AppendBuffer('\t');
				};
			};
		}
	}

	class : private : Scanner {
		@buffer : Char[];
		@buffer_index : Int;
		@cur_char : Char;
		@next_char : Char;
		@line_num : Int;
		@line_pos : Int;
		@keywords : Hash<String, Token>;
		@tokens : Vector<Token>;

		New(stream_in : String) {
			@buffer := stream_in->ToCharArray();
			@line_num := @line_pos := 1;
			@tokens := Vector->New()<Token>;

			@keywords := Hash->New()<String, Token>;
			
			@keywords->Insert("and", Token->New(@line_num, System.Utility.Token->Type->AND_ID, "and_id"));
			@keywords->Insert("or", Token->New(@line_num, System.Utility.Token->Type->OR_ID, "or_id"));
			@keywords->Insert("xor", Token->New(@line_num, System.Utility.Token->Type->XOR_ID, "xor_id"));
			@keywords->Insert("virtual", Token->New(@line_num, System.Utility.Token->Type->VIRTUAL_ID, "virtual_id"));
			@keywords->Insert("bundle", Token->New(@line_num, System.Utility.Token->Type->BUNDLE_ID, "bundle_id"));
			@keywords->Insert("interface", Token->New(@line_num, System.Utility.Token->Type->INTERFACE_ID, "interface_id"));
			@keywords->Insert("implements", Token->New(@line_num, System.Utility.Token->Type->IMPLEMENTS_ID, "implements_id"));
			@keywords->Insert("enum", Token->New(@line_num, System.Utility.Token->Type->ENUM_ID, "enum_id"));
			@keywords->Insert("consts", Token->New(@line_num, System.Utility.Token->Type->CONSTS_ID, "consts_id"));
			@keywords->Insert("reverse", Token->New(@line_num, System.Utility.Token->Type->REVERSE_ID, "reverse_id"));
			@keywords->Insert("Parent", Token->New(@line_num, System.Utility.Token->Type->PARENT_ID, "parent_id"));
			@keywords->Insert("from", Token->New(@line_num, System.Utility.Token->Type->FROM_ID, "from_id"));
			@keywords->Insert("true", Token->New(@line_num, System.Utility.Token->Type->TRUE_ID, "true_id"));
			@keywords->Insert("false", Token->New(@line_num, System.Utility.Token->Type->FALSE_ID, "false_id"));
			@keywords->Insert("New", Token->New(@line_num, System.Utility.Token->Type->NEW_ID, "new_id"));
			@keywords->Insert("As", Token->New(@line_num, System.Utility.Token->Type->AS_ID, "as_id"));
			@keywords->Insert("TypeOf", Token->New(@line_num, System.Utility.Token->Type->TYPE_OF_ID, "typeof_id"));
			@keywords->Insert("critical", Token->New(@line_num, System.Utility.Token->Type->CRITICAL_ID, "critical_id"));
			@keywords->Insert("use", Token->New(@line_num, System.Utility.Token->Type->USE_ID, "use_id"));
			@keywords->Insert("class", Token->New(@line_num, System.Utility.Token->Type->CLASS_ID, "class_id"));
			@keywords->Insert("function", Token->New(@line_num, System.Utility.Token->Type->FUNCTION_ID, "function_id"));
			@keywords->Insert("alias", Token->New(@line_num, System.Utility.Token->Type->ALIAS_ID, "alias_id"));
			@keywords->Insert("method", Token->New(@line_num, System.Utility.Token->Type->METHOD_ID, "method_id"));
			@keywords->Insert("public", Token->New(@line_num, System.Utility.Token->Type->PUBLIC_ID, "public_id"));
			@keywords->Insert("private", Token->New(@line_num, System.Utility.Token->Type->PRIVATE_ID, "private_id"));
			@keywords->Insert("native", Token->New(@line_num, System.Utility.Token->Type->NATIVE_ID, "native_id"));
			@keywords->Insert("static", Token->New(@line_num, System.Utility.Token->Type->STATIC_ID, "static_id"));
			@keywords->Insert("if", Token->New(@line_num, System.Utility.Token->Type->IF_ID, "if_id"));
			@keywords->Insert("else", Token->New(@line_num, System.Utility.Token->Type->ELSE_ID, "else_id"));
			@keywords->Insert("for", Token->New(@line_num, System.Utility.Token->Type->FOR_ID, "for_id"));
			@keywords->Insert("each", Token->New(@line_num, System.Utility.Token->Type->EACH_ID, "each_id"));
			@keywords->Insert("do", Token->New(@line_num, System.Utility.Token->Type->DO_ID, "do_id"));
			@keywords->Insert("while", Token->New(@line_num, System.Utility.Token->Type->WHILE_ID, "while_id"));
			@keywords->Insert("select", Token->New(@line_num, System.Utility.Token->Type->SELECT_ID, "select_id"));
			@keywords->Insert("label", Token->New(@line_num, System.Utility.Token->Type->LABEL_ID, "label_id"));
			@keywords->Insert("break", Token->New(@line_num, System.Utility.Token->Type->BREAK_ID, "break_id"));
			@keywords->Insert("continue", Token->New(@line_num, System.Utility.Token->Type->CONTINUE_ID, "continue_id"));
			@keywords->Insert("return", Token->New(@line_num, System.Utility.Token->Type->RETURN_ID, "return_id"));
			@keywords->Insert("leaving", Token->New(@line_num, System.Utility.Token->Type->LEAVING_ID, "leaving_id"));
			@keywords->Insert("String", Token->New(@line_num, System.Utility.Token->Type->STRING_ID, "string_id"));
			@keywords->Insert("Int", Token->New(@line_num, System.Utility.Token->Type->INT_ID, "int_id"));
			@keywords->Insert("Float", Token->New(@line_num, System.Utility.Token->Type->FLOAT_ID, "float_id"));
			@keywords->Insert("Char", Token->New(@line_num, System.Utility.Token->Type->CHAR_ID, "char_id"));
			@keywords->Insert("Bool", Token->New(@line_num, System.Utility.Token->Type->BOOL_ID, "bool_id"));
			@keywords->Insert("Byte", Token->New(@line_num, System.Utility.Token->Type->BYTE_ID, "byte_id"));
			@keywords->Insert("Nil", Token->New(@line_num, System.Utility.Token->Type->NIL_ID, "nil_id"));
			@keywords->Insert("other", Token->New(@line_num, System.Utility.Token->Type->OTHER_ID, "other_id"));
		}

		method : NextChar() ~ Nil {
			if(@buffer_index < @buffer->Size()) {
				@cur_char := @buffer[@buffer_index++];
				@line_pos += 1;

				if(@buffer_index < @buffer->Size()) {
					@next_char := @buffer[@buffer_index];
				}
				else {
					@next_char := '\0';
				};
			}
			else {
				@cur_char := next_char := '\0';
			};
		}

		method : Whitespace() ~ Nil {
			new_line_count := 0;

			while(@cur_char = ' ' | @cur_char = '\t' | @cur_char = '\r' | @cur_char = '\n') {
				if(@cur_char = '\n') {
					@line_num += 1;
					new_line_count += 1;
					@line_pos := 1;
				};

				NextChar();
			};
			
			if(new_line_count > 1) {
				for(i := 0; i < new_line_count; i += 2;) {
					@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->VTAB, "vtab"));
				};
			};
		}

		method : public : GetTokens() ~ Vector<Token> {
			return @tokens;
		}

		method : public : Scan() ~ Bool {
			NextChar();

			while(@cur_char <> '\0') {
				Whitespace();
				
				# comment
				if(@cur_char = '#') {
					NextChar();

					if(@cur_char = '~') {
						NextChar();
						start := @buffer_index - 1;
						while(<>(@cur_char = '~' & @next_char = '#')) {
							if(@cur_char = '\n') {
								@line_num += 1;
								@line_pos := 1;
							};

							NextChar();
						};
						end := @buffer_index;
						NextChar();
						NextChar();

						comment :=  String->New(@buffer, start, end - start - 1);
						@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->MULTI_COMMENT, comment));
					}
					else {
						start := @buffer_index - 1;
						while(<>@cur_char = '\n') {
							NextChar();
						};
						end := @buffer_index;
						NextChar();

						comment :=  String->New(@buffer, start, end - start - 1);
						@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->LINE_COMMENT, comment));
					};
				}
				# string literal
				else if(@cur_char = '"') {
					NextChar();				
					is_escape := false;

					start := @buffer_index - 1;
					while(is_escape | @cur_char <> '"') {
						if(is_escape) {
							is_escape := false;
						};

						if(@cur_char = '\\') {
							is_escape := true;
						};

						NextChar();
					};				
					end := @buffer_index;
					NextChar();

					string_lit :=  String->New(@buffer, start, end - start - 1);
					@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->STRING_LIT, string_lit));
				}
				# char const
				else if(@cur_char = '\'') {
					NextChar();				

					start := @buffer_index - 1;
					if(@cur_char <> '\\') {
						NextChar();
						if(@cur_char = '\\') {
							NextChar();
						};
					};				
					end := @buffer_index;
					NextChar();

					string_lit :=  String->New(@buffer, start, end - start - 1);
					@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->CHAR_LIT, string_lit));
				}
				# keyword or variable
				else if(@cur_char->IsChar() | @cur_char = '@' | @cur_char = '$') {
					start := @buffer_index - 1;
					while(@cur_char->IsChar() | @cur_char->IsDigit() | @cur_char = '_' | @cur_char = '@' | @cur_char = '.' | @cur_char = '$') {
						NextChar();
					};
					end := @buffer_index;
					word := String->New(@buffer, start, end - start - 1);
					
					keyword := @keywords->Find(word);
					if(keyword = Nil) {
						@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->IDENT, word));
					}
					else {
						@tokens->AddBack(keyword->Copy(@line_num));
					};
				}
				# number or digit
				else if(@cur_char->IsDigit() | @cur_char = '.' | (@cur_char = '+' & (@next_char->IsDigit()| @cur_char = '.')) | 
						@cur_char = '-' & (@next_char->IsDigit() | @cur_char = '.')) {
					start := @buffer_index - 1;

					if(@cur_char = '+' | @cur_char = '-') {
						NextChar();
					};

					while(@cur_char = '.' | @cur_char->IsDigit() | @cur_char = 'x' | @cur_char = 'X' | 
							(@cur_char >= 'a' & @cur_char <= 'f') | (@cur_char >= 'A' & @cur_char <= 'F')) {
						NextChar();
					};
					end := @buffer_index;

					num_lit := String->New(@buffer, start, end - start - 1);
					@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->NUM_LIT, num_lit));
				}
				else {
					select(@cur_char) {
						label '#' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->POUND, "pound"));
							NextChar();
						}

						label '@' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->AT, "at"));
							NextChar();
						}

						label '{' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->OCBRACE, "ocbrace"));
							NextChar();
						}

						label '}' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->CCBRACE, "ccbrace"));
							NextChar();
						}

						label '(' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->OPREN, "opern"));
							NextChar();
						}

						label ')' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->CPREN, "cpren"));
							NextChar();
						}

						label '_' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->DASH, "dash"));
							NextChar();
						}

						label '[' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->OBRACE, "obrace"));
							NextChar();
						}

						label ']' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->CBRACE, "cbrace"));
							NextChar();
						}

						label ',' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->COMMA, "COMMA"));
							NextChar();
						}

						label ':' {
							if(@next_char = '=') {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->ASGN, "asgn"));
								NextChar();
							}
							else {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->COLON, "colon"));
							};
							NextChar();
						}

						label '~' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->TILDE, "tilde"));
							NextChar();
						}

						label ';' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->SEMI, "semi"));
							NextChar();
						}

						label '+' {
							if(@next_char = '=') {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->ADD_ASN, "add_asn"));
								NextChar();
							}
							else if(@next_char = '+') {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->ADD_ADD, "add_add"));
								NextChar();
							}
							else {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->ADD, "add"));
							};
							NextChar();
						}

						label '-' {
							if(@next_char = '>') {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->ASSESSOR, "assessor"));
								NextChar();
							}
							else if(@next_char = '=') {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->SUB_ASN, "sub_asn"));
								NextChar();
							}
							else if(@next_char = '-') {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->SUB_SUB, "sub_sub"));
								NextChar();
							}
							else {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->SUB, "sub"));
							};
							NextChar();
						}

						label '→' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->ASSESSOR, "assessor"));
							NextChar();
						}

						label '←' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->ASGN, "asgn"));
							NextChar();
						}

						label '≠' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->NEQL, "neql"));
							NextChar();
						}

						label '≥' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->GTR_EQL, "gtr_eql"));
							NextChar();
						}
						
						label '≤' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->LESS_EQL, "less_eql"));
							NextChar();
						}

						label '\e' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->ESCAPE, "\e"));
							NextChar();
						}

						label '*' {
							if(@next_char = '=') {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->MUL_ASN, "mul_asn"));
								NextChar();
							}
							else {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->MUL, "mul"));
							};
							NextChar();
						}

						label '/' {
							if(@next_char = '=') {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->DIV_ASN, "div_asn"));
								NextChar();
							}
							else {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->DIV, "div"));
							};
							NextChar();
						}

						label '%' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->MOD, "mod"));
							NextChar();
						}

						label '&' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->AND, "and"));
							NextChar();
						}

						label '|' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->OR, "or"));
							NextChar();
						}

						label '?' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->QUESTION, "question"));
							NextChar();
						}

						label '\'' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->QUOTE, "quote"));
							NextChar();
						}

						label '\\' {
							@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->FWD_SLASH, "fwd_slash"));
							NextChar();
						}

						label '=' {
							if(@next_char = '>') {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->NEQL, "neql"));
								NextChar();
							}
							else {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->EQL, "eql"));
								NextChar();
							};
						}

						label '<' {
							if(@next_char = '>') {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->NEQL, "neql"));
								NextChar();
							}
							else if(@next_char = '=') {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->LAMBDA, "less_eql"));
								NextChar();
							}
							else {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->LESS, "less"));
							};
							NextChar();
						}

						label '>' {
							if(@next_char = '=') {
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->GTR_EQL, "gtr_eql"));
								NextChar();
							}
							else { 
								@tokens->AddBack(Token->New(@line_num, System.Utility.Token->Type->GTR, "gtr"));
							};
							NextChar();
						}

						other {
							if(@cur_char <> '\0') {
								id := @cur_char->As(Int);
								next_id := @next_char->As(Int);
								"\n--- [Other ({$@line_num},{$@line_pos}): '{$@cur_char}'({$id}), '{$@next_char}'({$next_id})] ---"->ErrorLine();						
								Runtime->Exit(1);
							};
						}
					};
				};
			};

			return true;
		}
	}

	class : private : Token {
		@line_num : Int;
		@type : Token->Type;
		@value : String;

		enum Type := -128 {
			AND_ID,
			OR_ID,
			XOR_ID,
			VIRTUAL_ID,
			BUNDLE_ID,
			INTERFACE_ID,
			IMPLEMENTS_ID,
			ENUM_ID,
			CONSTS_ID,
			REVERSE_ID,
			PARENT_ID,
			FROM_ID,
			TRUE_ID,
			FALSE_ID,
			NEW_ID,
			AS_ID,
			TYPE_OF_ID,
			CRITICAL_ID,
			LAMBDA,			
			USE_ID,
			DO_ID,
			FOR_ID,
			EACH_ID,
			BREAK_ID,
			CONTINUE_ID,
			ALIAS_ID,
			RETURN_ID,
			LEAVING_ID,
			NIL_ID,
			STRING_ID,
			INT_ID,
			FLOAT_ID,
			CHAR_ID,
			BOOL_ID,
			BYTE_ID,
			CLASS_ID,
			FUNCTION_ID,
			METHOD_ID,
			PUBLIC_ID,
			PRIVATE_ID,
			NATIVE_ID,
			STATIC_ID,
			STRING_LIT,
			CHAR_LIT,
			NUM_LIT,
			ELSE_ID,
			IF_ID,
			WHILE_ID,
			SELECT_ID,
			LABEL_ID,
			OTHER_ID,
			ASGN,
			CHAR,
			STR,
			VAR,
			DASH,
			OPREN,
			CPREN,
			OBRACE,
			CBRACE,
			OCBRACE,
			CCBRACE,
			ASSESSOR,
			AND,
			OR,
			QUESTION,
			DIV,
			MOD,
			MUL,
			ADD,
			SUB,
			DIV_ASN,
			MUL_ASN,
			ADD_ASN,
			SUB_ASN,
			ADD_ADD,
			SUB_SUB,
			COLON,
			SEMI,
			COMMA,
			TILDE,
			POUND,
			AT,
			QUOTE,
			FWD_SLASH,
			LESS,
			LESS_EQL,
			ESCAPE,
			GTR,
			GTR_EQL,
			EQL,
			NEQL,
			NOT,
			IDENT,
			VTAB,
			LINE_COMMENT,
			MULTI_COMMENT,
			EOS,
			OTHER
		}
		
		New(line_num : Int, type : Token->Type, value : String) {
			@line_num := line_num;
			@type := type;
			@value := value;
		}

		method : public : Copy(line_num : Int) ~ Token {
			return Token->New(line_num, @type, @value);
		}
		
		method : public : GetLine() ~ Int {
			return @line_num;
		}
		
		method : public : GetType() ~ Token->Type {
			return @type;
		}
		
		method : public : GetValue() ~ String {
			return @value;
		}
	}
}
