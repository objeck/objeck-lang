use Collection;

bundle Default {
	class Thingy {
		@id : Int;

		New(id : Int) {
			@id := id;
		}

		method : public : Print() ~ Nil {
			@id->PrintLine();
		}
	}

	class Person from Thingy {
		@name : String;
		@values : StringMap;

		New(id : Int, name : String) {
			Parent(id);
			@name := name;
			@values := StringMap->New();
			v := IntHolder->New(101);
			@values->Insert("Jason", v);
			v := IntHolder->New(9);
			@values->Insert("Mark", v);
		}

		method : public : Print() ~ Nil {
			@id->PrintLine();
			@name->PrintLine();
			v := @values->Find("Jason")->As(IntHolder);
			v->Get()->PrintLine();	
			v := @values->Find("Mark")->As(IntHolder);
			v->Get()->PrintLine();	
		}
	}

	class Serial {
		function : Main(args : String[]) ~ Nil {
			t := Thingy->New(7);
			p := Person->New(13, "Bush");

			s := System.IO.Serializer->New();
			s->Write(t);
			s->Write(p);

			writer := IO.File.FileWriter->New("objects.dat");			
			writer->WriteBuffer(s->Serialize());
			writer->Close();

			buffer := IO.File.FileReader->ReadBinaryFile("objects.dat");			
			d := System.IO.Deserializer->New(buffer);

			t2 := d->ReadObject()->As(Thingy);
			t2->Print();
			p2 := d->ReadObject()->As(Person);
			p2->Print();
		}
	}
}
