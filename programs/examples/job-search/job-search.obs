use System.IO.Filesystem, Web.HTTP.Server;

class Test {
	function : Main(args : String[]) ~ Nil {
		if(args->Size() = 1) {
			block := GetBlock(FileReader->ReadFile(args[0]));
			output := ParseBlock(block);
			FileWriter->WriteFile("out-2.html", output);
		};
	}

	function : GetBlock(input : String) ~ Char[] {
		start_pos := input->Find("<html");
		end_pos := input->Find("</html>")

		if(start_pos < end_pos) {
			return input->SubString(start_pos, end_pos - start_pos + "</html>"->Size())->ToCharArray();
		}

		return Nil;
	}

	function : ParseBlock(input : Char[]) ~ String {
		buffer := "";
		
		if(input <> Nil) {
			i := 0;
			char, next_char : Char;
			while(i < input->Size()) {
				char := input[i++];
				if(i < input->Size()) {
					next_char := input[i];
				}
				else {
					next_char := '\0';
				}

				# check
				if(char = '=' & (next_char->IsDigit() | next_char = '\r' | next_char = '\n' | (next_char >= 'A' & next_char <= 'F'))) {
					if(next_char = '\r') {
						i += 2;
					}
					else if(next_char = '\n') {
						i += 1;
					}
					else {
						num_buf := "0x";
						while(next_char->IsDigit() | (next_char >= 'A' & next_char <= 'F')) {
							num_buf += next_char;
							next_char := input[++i];
						};

						num := num_buf->ToInt();
						if(num >= 0x20 & num <= 0x7f) {
							buffer += num->As(Char);
						};

						if(num = 0xc2 & next_char = '=' & i + 2 < input->Size()) {
							num_buf := "0x";
							num_buf += input[i + 1];
							num_buf += input[i + 2];
							num := num_buf->ToInt();

							if(num > 0) {
								buffer += num->As(Char);
							};
							i += 3;
						};
					};
				}
				else {
					buffer += char;
				}
			};
		};

		return buffer;
	}
}