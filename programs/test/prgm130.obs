use Collection;
use System.IO;
use System.IO.Filesystem;
use System.IO.Net;

class Client {
	function : Main(args : String[]) ~ Nil {
		if(args->Size() = 1) {
			values := StringMap->New();
			values->Insert("name", "Objeck");
			values->Insert("age", IntHolder->New(10));
			values->Insert("pi", FloatHolder->New(3.145));

			serializer := Serializer->New();
			serializer->Write(values);
			serializer->Write("Fin.");
			bytes := serializer->Serialize();

			if(args[0]->Equals("socket")) {
				WriteSocket(bytes);
			}
			else {
				WriteFile(bytes);
			};
		};
	}

	function : WriteFile(bytes : Byte[]) ~ Nil {
		size := bytes->Size();
		file_name := Runtime->GetTempDir();
		file_name += "/data.dat";
		"client: file='{$file_name}', size={$size} bytes"->PrintLine();
		
		file := FileWriter->New(file_name);
		leaving {
			file->Close();
		};
		
		file->WriteBuffer(bytes);
	}

	function : WriteSocket(bytes : Byte[]) ~ Nil {
		client := TCPSocket->New("localhost", 4660);
		leaving {
			client->Flush();
			client->Close();
		};

		if(client->IsOpen()) {
			size := bytes->Size();
			size_str := size->ToString();
			client->WriteString(size_str);
			client->WriteBuffer(0, size, bytes)->PrintLine();
		};
	}	
}
