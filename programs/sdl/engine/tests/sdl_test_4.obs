use Game.SDL2;
use Game.Framework;
use Collection;

class Demo {
	@engine : GameFramework;
	@player : Player;
	@level : Level;

	function : Main(args : String[]) ~ Nil {
		Demo->New()->Run();
	}

	New() {
		@engine := GameFramework->New(Game->SCREEN_WIDTH, Game->SCREEN_HEIGHT, "Platformer Jumper");
		@engine->SetClearColor(Color->New(0, 0, 255));
	}

	method : Run() ~ Nil {
		if(@engine->IsOk()) {
			@player := Player->New(@engine->AddRectangle(40, 60));
			@level := Level->New(@player, @engine);

			quit := false;
			e := @engine->GetEvent();
			while(<>quit) {
				@engine->FrameStart();
				@engine->Clear();
				
				# process input
				while(e->Poll() <> 0) {
					if(e->GetType() = EventType->SDL_QUIT) {
						quit := true;
					};

					ProcessInput(e);
				};

				# update
				@player->Update();
				@level->Update();

				# check for screen edges
				if(@player->GetRight() > Game->SCREEN_WIDTH) {
		            @player->SetRight(Game->SCREEN_WIDTH);
		        };
		 
		        if(@player->GetLeft() < 0) {
		            @player->SetLeft(0);
		        };

				# draw
				@player->Draw();
				@level->Draw();

				# show
				@engine->Show();
				@engine->FrameEnd();
			};
		}
		else {
			"--- Error Initializing Game Environment ---"->ErrorLine();
			return;
		};

		leaving {
			@engine->Quit();
		};
	}

	method : public : ProcessInput(e : Event) ~ Nil {
		if(e->GetType() = EventType->SDL_KEYDOWN) {
	        select(e->GetKey()->GetKeysym()->GetScancode()) {    	
	            label Scancode->SDL_SCANCODE_LEFT: {
	            	@player->GoLeft();
	            }

	            label Scancode->SDL_SCANCODE_RIGHT: {
	            	@player->GoRight();
	            }

	            label Scancode->SDL_SCANCODE_UP: {
	            	@player->Jump();
	            }
	        };
	    }
	    else if(e->GetType() = EventType->SDL_KEYUP) {
	       	code := e->GetKey()->GetKeysym()->GetScancode();
	       	
	       	if(code = Scancode->SDL_SCANCODE_LEFT & @player->ChangeX() < 0) {
	       		@player->Stop();
	        };

	        if(code = Scancode->SDL_SCANCODE_RIGHT & @player->ChangeX() > 0) {
	        	@player->Stop();
	        };
	    };
	}
}

class Player {
	@rect : Rectangle;
	@change_x : Float;
	@change_y : Float;
	@platforms : Vector;

	New(rect : Rectangle) {
		@rect := rect;
		@rect->SetFill(true);
		@rect->SetColor(Color->New(255, 0, 0));
	}

	method : public : GetLeft() ~ Int {
		return @rect->GetX();
	}

	method : public : GetRight() ~ Int {
		return @rect->GetX2();
	}

	method : public : SetLeft(x : Int) ~ Nil {
		@rect->SetX(x);
	}

	method : public : SetRight(x2 : Int) ~ Nil {
		@rect->SetX2(x2);
	}

	method : public : SetLevel(level : Level)  ~ Nil {
		@platforms := level->GetPlatforms();
	}

	method : public : ChangeX()  ~ Int {
		return @change_x;
	}

	method : public : GoLeft()  ~ Nil {
		@change_x := -6;
	}

	method : public : GoRight()  ~ Nil {
		@change_x := 6;
	}

	method : public : Stop()  ~ Nil {
		@change_x := 0;
	}

	# TODO
	method : public : Jump()  ~ Nil {
		@rect->AddY(2);

		bottom := false;
		each(i : @platforms) {
        	platform := @platforms->Get(i)->As(Platform);
        	if(platform->Collides(@rect)) {
        		bottom := true;
        	};
		};
        @rect->AddY(-2);
 
        if(bottom | @rect->GetY2() >= Game->SCREEN_HEIGHT) {
			@change_y := -10;
        };
	}

	method : public : native : Update()  ~ Nil {
		Gravity();

		# Move left/right
        @rect->AddX(@change_x);
        each(i : @platforms) {
        	platform := @platforms->Get(i)->As(Platform);
        	if(platform->Collides(@rect)) {
        		# moving right
        		if(@change_x > 0) {
        			@rect->SetX2(platform->GetX());
                }
        		# moving left
                else if(@change_x < 0) {
                	@rect->SetX(platform->GetX2());
	            };
        	};
        };

		# Move up/down
        @rect->AddY(@change_y);
		each(i : @platforms) {
        	platform := @platforms->Get(i)->As(Platform);
        	if(platform->Collides(@rect)) {
        		# moving right
        		if(@change_y > 0) {
        			@rect->SetY2(platform->GetY());
                }
        		# moving left
                else if(@change_y < 0) {
                	@rect->SetY(platform->GetY2());
	            };
        	};
        };				
	}

	method : public : Draw()  ~ Nil {
		@rect->Render();
	}

	method : public : Gravity()  ~ Nil {
        if(@change_y = 0) {
            @change_y := 1;
        }
        else {
            @change_y += .35;
        };
 
        # See if we are on the ground.
        if(@rect->GetY() >= Game->SCREEN_HEIGHT - @rect->GetHeight() & @change_y >= 0) {
            @change_y := 0;
            @rect->SetY(Game->SCREEN_HEIGHT - @rect->GetHeight());
        };
	}
}

class Platform {
	@rect : Rectangle;

	New(x : Int, y : Int, rect : Rectangle) {
		@rect := rect;
		@rect->SetFill(true);
		@rect->SetColor(Color->New(0, 255, 0));
		@rect->Set(x, y);
	}

	method : public : GetX()  ~ Int {
		return @rect->GetX();
	}

	method : public : GetX2()  ~ Int {
		return @rect->GetX2();
	}

	method : public : GetY()  ~ Int {
		return @rect->GetY();
	}

	method : public : GetY2()  ~ Int {
		return @rect->GetY2();
	}

	method : public : Update()  ~ Nil {
	}

	method : public : Draw()  ~ Nil {
		@rect->Render();
	}

	method : public : Collides(rect : Rectangle) ~ Bool {
		return @rect->GetPosition()->Overlaps(rect->GetPosition());
	}
}

class Level {
	@player : Player;
	@platforms : Vector;

	New(player : Player, engine : GameFramework) {
		@player := player;
		@platforms := Vector->New();

		@platforms->AddBack(Platform->New(500, 500, engine->AddRectangle(210, 70)));
		@platforms->AddBack(Platform->New(200, 400, engine->AddRectangle(210, 70)));	
		@platforms->AddBack(Platform->New(600, 300, engine->AddRectangle(210, 70)));

		@player->SetLevel(@self);
	}

	method : public : GetPlatforms() ~ Vector {
		return @platforms;
	}

	method : public : Update()  ~ Nil {
		each(i : @platforms) {
			@platforms->Get(i)->As(Platform)->Update();
		};
	}

	method : public : Draw()  ~ Nil {
		each(i : @platforms) {
			@platforms->Get(i)->As(Platform)->Draw();
		};
	}
}

consts Game {
	SCREEN_WIDTH := 800,
	SCREEN_HEIGHT := 600
}