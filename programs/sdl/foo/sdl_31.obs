#~
Lesson 31
http:# lazyfoo.net/tutorials/SDL/31_scrolling_backgrounds/index.php
~#

use Game.SDL2;

class LTexture {
	@renderer : Renderer;
	@font : Font;
	@texture : Texture;
	@render_quad : Rect;

	New(renderer : Renderer, font : Font := Nil) {
		@renderer := renderer;
		@font := font;
		@render_quad := Rect->New();
	}

	method : public : LoadFromRenderedText(textureText : String, textColor : Color) ~ Bool {
		Free();

		# Render text surface
		textSurface := @font->RenderTextSolid(textureText, textColor);
		if(textSurface->IsNull()) {
			"Unable to render text surface!"->ErrorLine();
			return false;
		};

		texture := textSurface->CreateTexture(@renderer);
		@render_quad->SetW(textSurface->GetW());
		@render_quad->SetH(textSurface->GetH());
		
		@texture := texture;
		return @texture <> Nil;
	}

	# Loads image at specified path
	method : public : LoadFromFile(path : String) ~ Bool {
		Free();

		# Load image at specified path
		loadedSurface := Image->Load(path);
		if(loadedSurface->IsNull()) {
			"Unable to load surface"->ErrorLine();
			return false;
		};

		loadedSurface->SetColorKey(true, loadedSurface->GetPixelFormat()->MapRGB(0, 0xFF, 0xFF));

		texture := loadedSurface->CreateTexture(@renderer);
		if(texture->IsNull()) {
			"Unable to create texture!"->ErrorLine();
			return false;
		};
		@render_quad->SetW(loadedSurface->GetW());
		@render_quad->SetH(loadedSurface->GetH());
		
		loadedSurface->Free();

		@texture := texture;
		return @texture <> Nil;
	}

	# Deallocates texture
	method : public : Free() ~ Nil {
		if(@texture <> Nil)	{
			@texture->Destroy();
		};
	}

	method : public : SetColor(red : Int, green : Int, blue : Int) ~ Nil {
		@texture->SetColorMod(red, green, blue);
	}

	method : public : SetBlendMode(blending : BlendMode) ~ Nil {
		# Set blending function
		@texture->SetBlendMode(blending);
	}
	
	method : public : SetAlpha(alpha : Int) ~ Nil {
		# Modulate texture alpha
		@texture->SetAlphaMod(alpha );
	}

	# Renders texture at given point
	method : public : Render(x : Int, y : Int, clip : Rect := Nil, 
			angle : Float := 0.0, center : Point := Nil, flip : RendererFlip := RendererFlip->SDL_FLIP_NONE) ~ Nil {
		@render_quad->SetX(x);
		@render_quad->SetY(y);

		if(clip <> Nil)	{
			@render_quad->SetW(clip->GetW());
			@render_quad->SetH(clip->GetH());
		};

		@renderer->CopyEx(@texture, clip, @render_quad, angle, center, flip);
	}

	# Gets image dimensions
	method : public : GetWidth() ~ Int {
		return @render_quad->GetW();
	}

	method : public : GetHeight() ~ Int {
		return @render_quad->GetH();
	}
}

class Dot {
	@dot_texture : LTexture;
	@posX : Int;
	@posY : Int;
	@velX : Int; 
	@velY : Int;
	DOT_VEL : Int;

	New(dot_texture : LTexture) {
		@dot_texture := dot_texture;
		DOT_VEL := 10;
	}

	# Takes key presses and adjusts the dot's velocity
	method : public : HandleEvent(e : Event) ~ Nil {
		# If a key was pressed
		if(e->GetType() = EventType->SDL_KEYDOWN & e->GetKey()->GetRepeat() = 0) {
	        # Adjust the velocity
	        select(e->GetKey()->GetKeysym()->GetScancode()) {    	
	            label Scancode->SDL_SCANCODE_UP: {
	            	@velY -= DOT_VEL;
	            }

	            label Scancode->SDL_SCANCODE_DOWN: {
	            	@velY += DOT_VEL;
	            }

	            label Scancode->SDL_SCANCODE_LEFT: {
	            	@velX -= DOT_VEL;
	            }

	            label Scancode->SDL_SCANCODE_RIGHT: {
	            	@velX += DOT_VEL;
	            }        	            
	        };
	    }
	    # If a key was released
	    else if(e->GetType() = EventType->SDL_KEYUP & e->GetKey()->GetRepeat() = 0) {
	        # Adjust the velocity
	        select(e->GetKey()->GetKeysym()->GetScancode()) {
	        	label Scancode->SDL_SCANCODE_UP: {
	            	@velY += DOT_VEL;
	            }

	            label Scancode->SDL_SCANCODE_DOWN: {
	            	@velY -= DOT_VEL;
	            }

	            label Scancode->SDL_SCANCODE_LEFT: {
	            	@velX += DOT_VEL;
	            }

	            label Scancode->SDL_SCANCODE_RIGHT: {
	            	@velX -= DOT_VEL;
	            }
	        };
	    };   
	}

	# Moves the dot
	method : public : Move() ~ Nil {
		# Move the dot left or right
	    @posX += @velX;

	    # If the dot went too far to the left or right
	    if( ( @posX < 0 ) | ( @posX + @dot_texture->GetWidth() > Screen->WIDTH->As(Int) ) ) {
	        # Move back
	        @posX -= @velX;
	    };

	    # Move the dot up or down
	    @posY += @velY;

	    # If the dot went too far up or down
	    if( ( @posY < 0 ) | ( @posY + @dot_texture->GetHeight() > Screen->HEIGHT->As(Int) ) ) {
	        # Move back
	        @posY -= @velY;
	    };
	}

	# Shows the dot1 on the screen
	method : public : Render() ~ Nil {
		@dot_texture->Render(@posX, @posY);
	}

	method : public : LoadFromFile(file : String) ~ Bool {
		return @dot_texture->LoadFromFile(file);
	}

	method : public : Free() ~ Nil {
		@dot_texture->Free();
	}
}

consts Screen {
	WIDTH := 640,
	HEIGHT := 480,
	FPS := 60
}

class Test {
	@window : static : Window;
	@renderer : static : Renderer;
	@bg_texture : static : LTexture;
	@dot : static : Dot;
	
	function : Main(args : String[]) ~ Nil {
		# Start up SDL and create window
		if( <>Init() ) {
			"Failed to initialize!"->ErrorLine();
			return;
		};
		
		# Load media
		if( <>LoadMedia() ) {
			"Failed to load media!"->ErrorLine();
			return;
		};

		scrolling_offset := 0;

		FRAME_RATE := 1000 / Screen->FPS->As(Int);
			
		# Main loop flag
		quit := false;

		# Event handler
		e := Event->New();

		# While application is running
		while(<>quit) {
			start := Timer->GetTicks();

			# Handle events on queue
			while( e->Poll() <> 0 ) {
				# User requests quit
				if(e->GetType() = EventType->SDL_QUIT) {
					quit := true;
				};

				# Handle input for the dot
				@dot->HandleEvent(e);
			};

			@dot->Move();
			
			@renderer->SetDrawColor(0xFF, 0xFF, 0xFF, 0xFF);
			@renderer->Clear();


			# Scroll background
			scrolling_offset -= 1;
			if(scrolling_offset < -1 * @bg_texture->GetWidth()) {
				scrolling_offset := 0;
			};

			# Render background
			@bg_texture->Render(scrolling_offset, 0);
			@bg_texture->Render(scrolling_offset + @bg_texture->GetWidth(), 0);

			@dot->Render();

			# Update screen
			@renderer->Present();

			end := start - Timer->GetTicks();
			if(end < FRAME_RATE) {
				Timer->Delay(FRAME_RATE - end);
			};
			frame += 1;
		};

		e->Free();
		Close();
	}
	
	function : Init() ~ Bool {
		SCREEN_WIDTH := 640;
		SCREEN_HEIGHT := 480;

		# Initialize SDL
		if(Core->Init(HardwareFlags->SDL_INIT_VIDEO) < 0) {
			"SDL could not initialize!"->ErrorLine();
			return false;
		};
		
		if(<>Hints->Set("SDL_RENDER_SCALE_QUALITY", "1")) {
			"Warning: Linear texture filtering not enabled!"->ErrorLine();
			return false;
		};

		# Create window
		@window := Window->New("SDL Tutorial", 
			WindowFlags->SDL_WINDOWPOS_UNDEFINED, WindowFlags->SDL_WINDOWPOS_UNDEFINED, 
			Screen->WIDTH->As(Int), Screen->HEIGHT->As(Int), WindowFlags->SDL_WINDOW_SHOWN);
		if(@window->IsNull()) {
			"Cannot create window!"->ErrorLine();
			return false;
		};

		# Get window surface
		@renderer := Renderer->New(@window, -1, 
			RendererFlags->SDL_RENDERER_ACCELERATED and RendererFlags->SDL_RENDERER_PRESENTVSYNC);
		if(@renderer->IsNull()) {
			"Cannot create renderer!"->ErrorLine();
			return false;
		};
		@renderer->SetDrawColor(0xFF, 0xFF, 0xFF, 0xFF);

		img_flags := ImageFlags->IMG_INIT_PNG->As(Int);
		if((Image->Init(img_flags) and img_flags) = 0) {
			"SDL image could not initialize!"->ErrorLine();
			return false;
		};

		@bg_texture := LTexture->New(@renderer);
		@dot := Dot->New(LTexture->New(@renderer));
		
		return true;
	}
	
	function : LoadMedia() ~ Bool {
		if(<>@dot->LoadFromFile("images/dot.bmp")) {
			"Failed to load bg image"->ErrorLine();
			return false;
		};

		if(<>@bg_texture->LoadFromFile("images/bg.png")) {
			"Failed to load bg image"->ErrorLine();
			return false;
		};

		return true;
	}
	
	function : LoadTexture(path : String) ~ Texture {
		# Load image at specified path
		loadedSurface := Image->Load(path);
		if(loadedSurface->IsNull()) {
			"Unable to load surface"->ErrorLine();
			return Nil;
		};

		texture := loadedSurface->CreateTexture(@renderer);
		if(texture->IsNull()) {
			"Unable to create texture!"->ErrorLine();
			return Nil;
		};

		loadedSurface->Free();
		
		return texture;
	}
	
	function : Close() ~ Nil {
		@bg_texture->Free();
		@dot->Free();

		@renderer->Destroy();
		@window->Destroy();

		Image->Quit();
		Core->Quit();
	}
}