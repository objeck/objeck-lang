use System.IO.File;
use Collection.Generic;

bundle CodeGen {
	#~
	Token scanner
	~#
	class Scanner {
		@filename : String;
		@tokens : Vector<Token>;

		New(filename : String) {
			@filename := filename;
			@tokens := Vector->New()<Token>;
		}
		
		method : public : Scan() ~ Vector<Token> {
			reserved := Map->New()<String, Token>;
			# c keywords
			reserved->Insert("extern", Token->New(Token->Type->EXTERN_ID, "extern"));
			reserved->Insert("DECLSPEC", Token->New(Token->Type->DECLSPEC_ID, "DECLSPEC"));
			reserved->Insert("SDL2_GFXPRIMITIVES_SCOPE", Token->New(Token->Type->SDL2_GFXPRIMITIVES_SCOPE_ID, "SDL2_GFXPRIMITIVES_SCOPE"));
			reserved->Insert("SDLCALL", Token->New(Token->Type->SDLCALL_ID, "SDLCALL"));
			
			# c types
			reserved->Insert("const", Token->New(Token->Type->CONST_ID, "const"));
			reserved->Insert("int", Token->New(Token->Type->INT_ID, "int"));
			reserved->Insert("char", Token->New(Token->Type->CHAR_ID, "char"));
			reserved->Insert("void", Token->New(Token->Type->VOID_ID, "void"));
			reserved->Insert("Uint8", Token->New(Token->Type->UINT8_ID, "Uint8"));
			reserved->Insert("Uint16", Token->New(Token->Type->UINT16_ID, "Uint16"));
			reserved->Insert("Sint16", Token->New(Token->Type->SINT16_ID, "Sint16"));
			reserved->Insert("Uint32", Token->New(Token->Type->UINT32_ID, "Uint32"));
			reserved->Insert("SDL_bool", Token->New(Token->Type->SDL_BOOL_ID, "SDL_bool"));
			
			"filename='{$@filename}'"->PrintLine();

			reader := FileReader->New(@filename);
			line_num := 0;
			while(reader->IsEOF() <> true) {
				line := reader->ReadLine();
				line_num := line_num + 1;

				i := 0;
				while(i < line->Size()) {
					# skip whitespace
					c := line->Get(i);
					while((c = ' ' | c = '\t' | c = '\r'| c = '\n') & i < line->Size()) {
						i += 1;
						c := line->Get(i);
					};

					# parse
					if(i < line->Size()) {
						# keyword
						if(line->Get(i)->IsChar()) {
							ident := "";
							c := line->Get(i);
							while(c->IsChar() | c->IsDigit() | c = '_') {
								ident->Append(line->Get(i));
								i += 1;
								c := line->Get(i);
							};
							
							result := reserved->Find(ident);
							if(result <> Nil) {
								@tokens->AddBack(result);
							}
							else {
								@tokens->AddBack(Token->New(Token->Type->IDENT, ident));
							};
						}
						# number
						else if(line->Get(i)->IsDigit()) {
							number := "";
							c := line->Get(i);
							while(c->IsDigit()) {
								number->Append(line->Get(i));
								i += 1;
								c := line->Get(i);
							};
							@tokens->AddBack(Token->New(Token->Type->NUM, number));
						}
						# other
						else {
							select(line->Get(i)) {
								label '(': {
									@tokens->AddBack(Token->New(Token->Type->OPRN, "("));
								}
								
								label ')': {
									@tokens->AddBack(Token->New(Token->Type->CPRN, ")"));
								}

								label '[': {
									@tokens->AddBack(Token->New(Token->Type->OBR, "["));
								}

								label ']': {
									@tokens->AddBack(Token->New(Token->Type->CBR, "]"));
								}

								label ',': {
									@tokens->AddBack(Token->New(Token->Type->COMMA, ","));
								}
								
								label ';': {
									@tokens->AddBack(Token->New(Token->Type->SCOLON, ";"));
								}

								label '*': {
									@tokens->AddBack(Token->New(Token->Type->STAR, "*"));
								}
								
								label '&': {
									@tokens->AddBack(Token->New(Token->Type->AND, "*"));
								}
								
								other: {
									@tokens->AddBack(Token->New(Token->Type->UNKWN, "?"));
								}
							};
							# update
							i += 1;
						};
					};
				};
			};
			reader->Close();
			
			@tokens->AddBack(Token->New(Token->Type->EOS));
			return @tokens;
		}
	}
	
	#~
	Scanned token
	~#
	class Token {
		enum Type := -100 {
			EXTERN_ID,
			DECLSPEC_ID,
			SDL2_GFXPRIMITIVES_SCOPE_ID,
			SDLCALL_ID,
			INT_ID,
			CHAR_ID,
			UINT8_ID,
			UINT16_ID,
			SINT16_ID,
			UINT32_ID,
			SDL_BOOL_ID,
			VOID_ID,
			CONST_ID,
			OPRN,
			CPRN,
			OBR,
			CBR,
			COMMA,
			SCOLON,
			STAR,
			AND,
			NUM,
			IDENT,
			UNKWN,
			EOS
		}
		
		@type : Token->Type;
		@value : String;

		New(type : Token->Type) {
			@type := type;
		}
		
		New(type : Token->Type, value : String) {
			@type := type;
			@value := value;
		}

		method : public : GetType() ~ Token->Type {
			return @type;
		}
		
		method : public : GetValue() ~ String {
			return @value;
		}
	}
}
