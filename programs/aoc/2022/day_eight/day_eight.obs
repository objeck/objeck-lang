class AoC {
	@file : String;
	@forest : Int[,];
	@debug : Bool;

	function : Main(args : String[]) ~ Nil {
		if(args->Size() = 1) {
			AoC->New(args[0])->Brun();
		};
	}

	New(file : String) {
		@file := file;
		@debug := false;
	}

	method : public : Brun() ~ Nil {
		inputs := ReadInput(@file)<String>;
		if(MakeForest(inputs)) {
			dims := @forest->Size();
			x := dims[0]; y := dims[1];

			"Forest is {$x}x{$y}\n---"->PrintLine();
			ViewableTrees()->PrintLine();
		}
		else {
			">>> Error executing program <<<"->ErrorLine();
		};
	}

	method : native : ViewableTrees() ~ Int {
		dims := @forest->Size();
		x := dims[0]; y := dims[1];

		count := 0;

		each(i : x) {
			each(j : y) {
				if(ViewableTree(@forest[i,j], i, j)) {
					count += 1;
				};
			};
		};

		return count;
	}

	method : ViewableTree(check : Int, x : Int, y : Int) ~ Bool {
		dims := @forest->Size();
		max := dims[0];

		# down
		dx := x + 1;
		result := true;
		while(result & dx < max) {
			result := @forest[dx,y] < check;
			dx += 1;
		};
		if(result) {
			return true;
		};
		
		# right
		dy := y + 1;
		result := true;
		while(result & dy < max) {
			result := @forest[x,dy] < check;
			dy += 1;
		};
		if(result) {
			return true;
		};

		# up
		dx := x - 1;
		result := true;
		while(result & dx >= 0) {
			result := @forest[dx, y] < check;
			dx -= 1;
		};
		if(result) {
			return true;
		};

		# left
		dy := y - 1;
		result := true;
		while(result & dy >= 0) {
			result := @forest[x, dy] < check;
			dy -= 1;
		};
		if(result) {
			return true;
		};
		
		return false;
	}

	method : MakeForest(inputs : Collection.Generic.Vector<String>) ~ Bool {
		if(<>inputs->IsEmpty()) {
			row := inputs->Get(0);
			if(row->Size() = inputs->Size()) {
				size := inputs->Size();
				@forest := Int->New[size, size];
				each(i : size) {
					row := inputs->Get(i);
					each(j : row) {
						@forest[i,j] := row->Get(j) - '0';
					};
				};				
			};

			return true;
		};

		return false;
	}

	function : ReadInput(file : String) ~ Collection.Generic.Vector<String> {
		inputs := Collection.Generic.Vector->New()<String>;

		reader := System.IO.Filesystem.FileReader->New(file);
		leaving {
			reader->Close();
		};

		line := reader->ReadLine();
		while(line <> Nil) {
			if(<>line->IsEmpty()) {
				inputs->AddBack(line);
			};
			line := reader->ReadLine();
		};

		return inputs;
	}
}