use API.Onnx, use API.OpenCV, System.IO.Filesystem, Collection;

class Test {
	function : Main(args : String[]) ~ Nil {
		if(args->Size() = 1) {
			image_path := args[0];
			
			ProcessYoloImages(image_path);
#			ProcessResNetImages(image_path)
		};
	}

	# Yolo-v5
	function : ProcessYoloImages(root_dir : String) ~ Nil {
		"### ProcessYoloImages ###"->PrintLine();

		image_path := root_dir + "/images/";
		image_path := image_path->ReplaceAll('\\', '/');

		model_path := root_dir + "/yolo11x.onnx";
		model_path := model_path->ReplaceAll('\\', '/');

		label_path := root_dir + "/yolo_labels.txt";
		label_path := label_path->ReplaceAll('\\', '/');

		"image_path={$image_path}"->PrintLine();
		"model_path={$model_path}"->PrintLine();
		"label_path={$label_path}"->PrintLine();

		image_labels := LoadLabels(label_path);
		image_label_size := image_labels->Size();
		"image_label_size={$image_label_size}"->PrintLine();

		# set image
		image_path += "skiing.jpg";

		image_bytes := FileReader->ReadBinaryFile(image_path);
		image_size := image_bytes->Size();

		"image_path={$image_path}"->PrintLine();
		"image_size={$image_size}\n"->PrintLine();

		image := Image->Load(image_bytes);
		if(image = Nil) {
			"--- Error: Unable to load image ---"->ErrorLine();
			return;
		};

		label_names := image_labels->ToArray()->As(String[]);
		result := API.Onnx.Runtime->YoloInference(image_bytes, 640, 640, model_path, 0.65, label_names);
		if(result = Nil) {
			"--- Error: No results ---"->ErrorLine();
			return;
		};

		result->ToString()->PrintLine();
		"==="->PrintLine();

		classifications := result->GetClassifications();
		each(classification in classifications) {
			label_str := classification->GetName();
			class_str := classification->ToString();
			"name='{$label_str}', class={$class_str}\n---"->PrintLine();

			bounds := classification->GetBounds();
			left := bounds->GetX();
			top := bounds->GetY();

			image := image->DrawRectangle(Rect->New(
				left, top, bounds->GetWidth(), bounds->GetHeight()), 
				Scalar->New(0, 255, 0), 2);

			image := image->DrawText(
				label_str,
				Point->New(left, top), 
				Font->FONT_HERSHEY_SIMPLEX,
				0.75,
				Scalar->New(255, 0, 0));
		};

		image->Show("Image w/ Bounds");
	}

	# ResNet-34
	function : ProcessResNetImages(root_dir : String) ~ Nil {
		"### ProcessResNetImages ###"->PrintLine();

		image_path := root_dir + "/images/";
		image_path := image_path->ReplaceAll('\\', '/');

		model_path := root_dir + "/resnet34-v2-7.onnx";
		model_path := model_path->ReplaceAll('\\', '/');

		label_path := root_dir + "/resnet_labels.txt";
		label_path := label_path->ReplaceAll('\\', '/');

		"image_path={$image_path}"->PrintLine();
		"model_path={$model_path}"->PrintLine();
		"label_path={$label_path}"->PrintLine();

		image_labels := LoadLabels(label_path);
		image_label_size := image_labels->Size();
		"image_label_size={$image_label_size}"->PrintLine();

		image_path += "dog.jpg";
				
		image_bytes := FileReader->ReadBinaryFile(image_path);
		image_size := image_bytes->Size();

		"image_path={$image_path}"->PrintLine();
		"image_size={$image_size}"->PrintLine();

		label_names := image_labels->ToArray()->As(String[]);
		result := API.Onnx.Runtime->ResNetInference(image_bytes, 224, 224, model_path, label_names);
		if(result <> Nil) {
			result->GetId()->PrintLine();
			result->GetName()->PrintLine();
			result->GetConfidence()->PrintLine();
			"--- Ok ---"->PrintLine();
		}
		else {
			"--- Error: No results returned ---"->ErrorLine();
		};
	}

	function : LoadLabels(label_path : String) ~ Vector<String> {
		label_names := Vector->New()<String>;

		file_in := FileReader->New(label_path);
		while(<>file_in->IsEoF()) {
			data_label := file_in->ReadLine();
			label_names->AddBack(data_label);
		};
		file_in->Close();

		return label_names;
	}
}
