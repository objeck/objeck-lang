use API.OpenAI, API.OpenAI.Chat, Collection;

class OpenAICompletion {
	@is_pplx : static : Bool;

	function : Main(args : String[]) ~ Nil {
		if(args->Size() <> 1) {
			">>> Error: Token file required <<"->ErrorLine();
			Runtime->Exit(1);
		};

		token := GetApiKey(args[0]);
		if(token = Nil) {
			">>> Unable to API key <<"->PrintLine();
			Runtime->Exit(1);
		};

		model : String;
		if(@is_pplx) {
			Completion->SetBaseUrl("https://api.perplexity.ai");
			model := "llama-3-sonar-small-32k-online";
		}
		else {
			model := "gpt-4o";
		}

		message := Pair->New("user", "What is the longest road in Denver?")<String, String>;
		completion := Completion->Complete(model, message, token);
		if(completion <> Nil) {
			choice := completion->GetFirstChoice();
			if(choice = Nil) {
				">>> Error: Unable to complete query <<<"->ErrorLine();
				Runtime->Exit(1);
			};

			message := choice->GetMessage()<String, String>;
			if(message = Nil) {
				">>> Error: Unable to read response <<"->ErrorLine();
				Runtime->Exit(1);
			};

			message->GetSecond()->PrintLine();
		};
	}

	function : GetApiKey(filename : String) ~ String {
		token := System.IO.Filesystem.FileReader->ReadFile(filename);
		if(token <> Nil) {
			token := token->Trim();
			if(<>token->StartsWith("sk-") & <>token->StartsWith("pplx-")) {
				">>> Unable to read token from file: '{$filename}' <<"->PrintLine();
				Runtime->Exit(1);
			};

			if(token->StartsWith("pplx-"))  {
				@is_pplx := true;
			};

			return token;
		};

		return Nil;
	}
}