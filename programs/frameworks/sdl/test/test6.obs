use Game.SDL2;
use Game.Framework;

class War {
	@framework : GameFramework;
	@missle : Missle;
	@color_black : Color;
	@mountain : Collection.Vector<Rocks>;
	@is_debug : Bool;

	function : Main(args : String[]) ~ Nil {
		War->New()->Run();
	}
	
	New() {
		@framework := GameFramework->New(GameConsts->SCREEN_WIDTH, GameConsts->SCREEN_HEIGHT, "War!");
		@is_debug := true;

		@color_black := Color->New(0, 0, 0);

		@framework->SetClearColor(@color_black);

		@color_blue := Color->New(14, 248, 248);

		id := 0;
		half_hor_max := 20;
		hort_rock_pos := 0.0;
		@mountain := Collection.Vector->New()<Rocks>;
		each(i : half_hor_max) {
			hort_rock_pos += 0.75;
			hort_rock_len := Int->Random(hort_rock_pos + 1, hort_rock_pos + 4);
			rocks := Rock->New[hort_rock_len];

			each(j : hort_rock_len) {
				rock_sprite := @framework->AddRectangle(20, 20);
				rock_sprite->SetColor(@color_blue);
				rock_sprite->SetPostion(Position->New(j * 20, i * 20, 20, 20));
				rocks[j] := Rock->New(id++, rock_sprite);
			};

			@mountain->AddBack(Rocks->New(rocks));
		};

		@missle := Missle->New(0.0125, 2.25, @framework->AddRectangle(6, 6));
	}
	
	method : Run() ~ Nil {
		if(@framework->IsOk()) {
			e := @framework->GetEvent();
			
			frame_count := 0;
			quit := false;

			while(<>quit) {
				# process input
				while(e->Poll() <> 0) {
					# quit event
					if(e->GetType() = EventType->SDL_QUIT) {
						quit := true;
					}
					# left mouse button event
					else if(e->GetMButton() <> Nil & e->GetMButton()->GetButton() = MouseButtonValue->SDL_BUTTON_LEFT) {
						point := Point->New();
						Cursor->GetMouseState(point);

						mouse_x := point->GetX();
						mouse_y := point->GetY();

						each(i : @mountain) {
							rocks := @mountain->Get(i)->GetRocks();
							each(j : rocks) {
								rock := rocks[j];
								
								if(<>rock->IsDestroyed() & rock->GetSprite()->GetPosition()->IntersectsPoint(mouse_x, mouse_y)) {
									rock->ReduceStrength(Rock->Strength->MAX);
									soundings := GetSoundings(i , j);
									each(k : soundings) {
										sounding := soundings[k];

										if(sounding <> Nil) {
											sounding->ReduceStrength(Int->Random(Rock->Strength->MAX * 0.25, Rock->Strength->MAX));
										};
									};
								};
							};
						};  	
					};
				};

				# render
				@framework->FrameStart();
				Render(frame_count);
				@framework->FrameEnd();

				# update frame count
				frame_count += 1;
				if(frame_count >= @framework->GetFps()) {
					frame_count := 0;
				};
			};		
		}
		else {
			"--- Error Initializing Environment ---"->ErrorLine();
			return;
		};

		leaving {
			@framework->Quit();
		};
	}

	method : GetSoundings(x : Int, y : Int) ~ Rock[] {
		soundings := Rock->New[8];

		# up
		soundings[0] := GetRock(x - 1, y);

		# up/right
		soundings[5] := GetRock(x + 1, y - 1);

		# right
		soundings[2] := GetRock(x, y + 1);

		# down/right
		soundings[3] := GetRock(x + 1, y + 1);

		# down
		soundings[4] := GetRock(x + 1, y);

		# down/left
		soundings[1] := GetRock(x - 1, y + 1);

		# left
		soundings[6] := GetRock(x, y - 1);

		# up/left
		soundings[7] := GetRock(x - 1, y - 1);

		return soundings;
	}

	method : GetRock(x : Int, y : Int) ~ Rock {
		if(x > -1 & x < @mountain->Size()) {
			rocks := @mountain->Get(x)->GetRocks();
			if(y > -1 & y < rocks->Size()) {
				return rocks[y];
			};
		};

		return Nil;
	}

	method : Render(frame_count : Int) ~ Nil {
		@framework->Clear();

		each(level in @mountain) {
			rocks := level->GetRocks();
			each(rock in rocks) {
				if(<>rock->IsDestroyed()) {
					rock->Render(frame_count);
				};
			};
		};
		
		@missle->Render(frame_count);
		
		@framework->Show();
	}
}

class Rocks {
	@rocks : Rock[];

	New(rocks : Rock[]) {
		@rocks := rocks;
	}

	method : public : GetRocks() ~ Rock[] {
		return @rocks;
	}

	method : public : ToString() ~ String {
		buffer := "[Rocks: ";

		each(rock in @rocks) {
			buffer += rock->ToString();
			buffer += '\n';
		};
		buffer += ']';

		return buffer;
	}
}

class Rock {
	@id : Int;
	@strength : Int;
	@sprite : Rectangle;
	@green_color : Color;

	consts Strength {
		MAX := 15
	}

	New(id : Int, sprite : Rectangle) {
		@id := id;
		@strength := Rock->Strength->MAX;
		@sprite := sprite;
		@green_color := Color->New(166, 44, 43);
	}

	method : public : GetSprite() ~ Rectangle {
		return @sprite;
	}

	method : public : GetId() ~ Int {
		return @id;
	}

	method : public : IsDestroyed() ~ Bool {
		return @strength <= 0;
	}

	method : public : ReduceStrength(change : Int) ~ Nil {
		if(@strength > 0) {
			@strength -= change;
		};
	}

	method : public : Render(frame_count : Int) ~ Nil {
		if(@strength < Rock->Strength->MAX / 2) {
			@sprite->SetColor(@green_color);
		};

		@sprite->Render();
	}

	method : public : ToString() ~ String {
		return "Rock: id={$@id}";
	}
}

class Missle {
	@sprite : Rectangle;
	@color_gray : Color;
	@missle_x, @missle_y : Float;
	@missle_trajectory, @missle_speed : Float;

	consts Strength {
		MAX := 15
	}

	New(missle_trajectory : Float, missle_speed : Float, sprite : Rectangle) {
		@missle_trajectory := missle_trajectory;
		@missle_speed := missle_speed;

		@missle_x := GameConsts->SCREEN_WIDTH * 0.75;
		@missle_y := GameConsts->SCREEN_HEIGHT - 7.0;

		@sprite := sprite;
		@color_gray := Color->New(113, 121, 126);
	}

	method : public : GetSprite() ~ Rectangle {
		return @sprite;
	}

	method : public : IsDestroyed() ~ Bool {
		return false;
	}

	method : public : Render(frame_count : Int) ~ Nil {
		@missle_x -= @missle_speed;
		@missle_y += @missle_trajectory;
		@sprite->SetCenter(@missle_x, Float->Sin(@missle_y) * 220 + GameConsts->SCREEN_HEIGHT);

		@sprite->SetColor(@color_gray);
		@sprite->SetFill(true);
		@sprite->Render();
	}
}

consts GameConsts {
	SCREEN_WIDTH := 1000,
	SCREEN_HEIGHT := 400
}